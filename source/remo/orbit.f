C
C
C**** *ORBIT* - FOR SOLAR ORBITAL PARAMETERS.
C
C     J.F.GELEYN     E.C.M.W.F.     03/06/82.
C
C     PURPOSE.
C     --------
C
C          THIS ROUTINE COMPUTES THE SOLAR CONSTANT NORMALISED BY ITS
C     ANNUAL MEAN AND THREE ORBITAL PARAMETERS DEPENDING ON THE TIME
C     OF THE DAY AS WELL AS OF THE YEAR (BOTH IN RADIANS). TIME SCALE
C     ORIGIN IS 1900/01/01 00 GMT. ALSO RETURNED IS A CONSTANT FOR THE
C     EFFECT OF THE EARTH'S CURVATURE ON THE COSINE OF THE SOLAR ZENITH
C     ANGLE.
C
C**   INTERFACE.
C     ----------
C
C          *ORBIT* IS CALLED FROM *PHYSC* AT THE FIRST LATITUDE ROW.
C          THERE ARE SEVEN DUMMY ARGUMENTS: *PCLOCK* IS THE TIME OF THE
C     DAY
C                                           *PYTIME* IS THE TIME OF THE
C     YEAR (BOTH IN RADIANS).
C                                           *PDISSE* IS THE RATIO OF THE
C     SOLAR CONSTANT TO ITS ANNUAL MEAN
C                                           *PZEN1*, *PZEN2* AND *PZEN3*
C     ARE ZENITHAL PARAMETERS.
C                                           *PRAE* IS THE RATIO OF THE
C     HEIGHT OF THE EQUIVALENT ATMOSPHERE TO THE RADIUS OF THE EARTH.
C
C     METHOD.
C     -------
C
C          STAIGHTFORWARD. INTERMEDIATE VARIABLES ARE THE SOLAR
C     DECLINATION AND THE EQUATION OF TIME.
C
C     EXTERNALS.
C     ----------
C
C          NONE.
C
C     REFERENCE.
C     ----------
C
C          NONE.
C
C
C*    DATA STATEMENTS.
C     ---- -----------
C
C          *ZCDIS*, *ZCDEC* AND *ZCEQT* ARE ARRAYS FOR A SECOND ORDER
C     *FOURIER DEVELOPMENT FOR RESPECTIVELY: SOLAR CONSTANT, SOLAR
C     DECLINATION AND EQUATION OF TIME. *ZRAE* IS THE VALUE FOR *PRAE*.
C
      SUBROUTINE ORBIT (PCLOCK,PYTIME,PDISSE,PZEN1,PZEN2,PZEN3,PRAE)
C
      IMPLICIT NONE
C
C     Dummy Arguments
C
      REAL, INTENT(IN)  :: PCLOCK,PYTIME
      REAL, INTENT(OUT) :: PDISSE,PZEN1,PZEN2,PZEN3,PRAE
C
C     Local Variables
C
      REAL :: ZCDIS(5),ZCDEC(5),ZCEQT(5)
      REAL :: ZC1YT, ZC2YT, ZCLOCK, ZDECLI, ZDISSE, ZEQTIM, ZRAE, ZS1YT, 
     &        ZS2YT, ZYTIME, ZZEN1, ZZEN2, ZZEN3
C
      DATA ZCDIS/+1.000110,+0.034221,+0.001280,+0.000719,+0.000077/
      DATA ZCDEC/+0.006918,-0.399912,+0.070257,-0.006758,+0.000907/
      DATA ZCEQT/+0.000075,+0.001868,-0.032077,-0.014615,-0.040849/
      DATA ZRAE/+.1277E-02/
C
C     ------------------------------------------------------------------
C
C*         1.     PRELIMINARY SETTING.
C                 ----------- --------
C
      ZCLOCK=PCLOCK
      ZYTIME=PYTIME
C
C     ------------------------------------------------------------------
C
C*         2.     COMPUTATIONS.
C                 -------------
C
      ZC1YT=COS(ZYTIME)
      ZS1YT=SIN(ZYTIME)
      ZC2YT=ZC1YT**2-ZS1YT**2
      ZS2YT=2.*ZS1YT*ZC1YT
      ZDISSE=ZCDIS(1)+ZCDIS(2)*ZC1YT+ZCDIS(3)*ZS1YT+ZCDIS(4)*ZC2YT
     &      +ZCDIS(5)*ZS2YT
      ZDECLI=ZCDEC(1)+ZCDEC(2)*ZC1YT+ZCDEC(3)*ZS1YT+ZCDEC(4)*ZC2YT
     &      +ZCDEC(5)*ZS2YT
      ZEQTIM=ZCEQT(1)+ZCEQT(2)*ZC1YT+ZCEQT(3)*ZS1YT+ZCEQT(4)*ZC2YT
     &      +ZCEQT(5)*ZS2YT
      ZZEN1=SIN(ZDECLI)
      ZZEN2=COS(ZDECLI)*COS(ZCLOCK+ZEQTIM)
      ZZEN3=COS(ZDECLI)*SIN(ZCLOCK+ZEQTIM)
C
C     ------------------------------------------------------------------
C
C*         3.     RETURN.
C                 -------
C
      PDISSE=ZDISSE
      PZEN1=ZZEN1
      PZEN2=ZZEN2
      PZEN3=ZZEN3
      PRAE=ZRAE
C
      RETURN
      END SUBROUTINE ORBIT
