C
C     SUBROUTINE CONGRA
C
C**** CONGRA   -   UP:LOESUNG EINER SPEZIELLEN DGL.
C**   AUFRUF   :   CALL CONGRA (X,RSE,RS2,XNUE,M,N,
C**               1AP,BBP,COEFP,BPTP,BBH,COEFH,BPTH,ACLDCQ,
C**               2ALF,CLF,BBF,APF,CPF,W1,W2)
C**   ENTRIES  :   KEINE
C**   ZWECK    :   LOESUNG EINER SPEZIELLEN NICHT SEPARIERBAREN PAR-
C**                TIELLEN DIFFERENTIALGLEICHUNG IN KUGELKOORDINATEN
C**                MITTELS DER CONJUGATE GRADIENT METHODE
C**   VERSIONS-
C**   DATUM    :   30.05.1990
C**
C**   EXTERNALS:   SOLVE
C**
C**   EINGABE-
C**   PARAMETER:   RSE(M,N): RECHTE SEITE DER GLEICHUNG
C**                X(M,N)=0: NULLTE NAEHERUNG DER LOESUNG
C**   AUSGABE-
C**   PARAMETER:   X(M,N)  : LOESUNGFELD
C**
C**   COMMON-
C**   BLOECKE  :   ORG   , PARAM
C**
C**   METHODE  :   CG-METHODE (CONJUGATE GRADIENT) IN VERBINDUNG MIT
C**                FFT IN W-E- UND GAUSS-ELIMINATION IN S-N-RICHTUNG
C**                RANDWERTE FUER SOLVE SIND IMMER NULL
C**
C**   FEHLERBE-
C**   HANDLUNG :   GENUEGEND (NUE) ITERATIONEN
C**
C**   VERFASSER:   P.PROHL
C
      SUBROUTINE CONGRA(X,RSE,RS2,XNUE,
     &   AP,BBP,COEFP,BPTP,BBH,COEFH,BPTH,ACLDCQ,ALF,CLF,
     &   BBF,APF,CPF,W1,W2,ZF_FFT,ZX_GAUSS,TRIGSI,RZ1I,RZ2I,
     &   IFAXI,NFFT,MGAUSS,MT,JOFF)
C
      IMPLICIT NONE
C
      INCLUDE "parorg.h"
      INCLUDE "org.h"
C
C     Dummy Arguments
C
      INTEGER, INTENT(IN)    :: NFFT,MGAUSS,MT,JOFF
      REAL,    INTENT(INOUT) :: X(IE,JE), XNUE(IE,JE)
      REAL,    INTENT(IN)    :: RSE(IE,JE)
      REAL,    INTENT(IN)    :: AP(MOJE),BBP(MOJE),COEFP(IE,MOJE),
     &                          BPTP(IE,MOJE)
      REAL,    INTENT(IN)    :: BBH(MOJE),COEFH(IE,MOJE),BPTH(IE,MOJE),
     &                          ACLDCQ(JE)
      REAL,    INTENT(IN)    :: ALF(IE,JE),CLF(IE,JE),BBF(IE,JE)
      REAL,    INTENT(IN)    :: APF(IE,JE),CPF(IE,JE)
      REAL,    INTENT(INOUT) :: W1(MOIE,JE),W2(MOIE,JE)
      REAL,    INTENT(IN)    :: TRIGSI(5*(MOIE-1)/2), RZ1I(6), RZ2I(6)
      INTEGER, INTENT(IN)    :: IFAXI(11)
      REAL,    INTENT(INOUT) :: ZF_FFT(MOIE,JE), ZX_GAUSS(MOIE,JE)
C
      REAL,    INTENT(INOUT) :: RS2(IE,JE)    
C
C     Local Variables
C
      INTEGER :: I, J, KK, NUE
CKS
C     FOR HALO EXCHANGE
      INTEGER :: KSIZE(10), ITYPE
C
      INCLUDE "haloexchorg"
CKS
      CALL PWAIT

      DO NUE = 1 , 20
         DO J = JAH , JEH
            DO I = IAH , IEH
               RS2(I,J) = X(I,J)
            ENDDO
         ENDDO

C     KK = 1: BERECHNUNG DER NUETEN     NAEHERUNG EINER POISSONGLEICHUNG
C     RECHTE SEITE = X(NUE)
C     KK = 2: BERECHNUNG DER NUETEN     NAEHERUNG EINER POISSONGLEICHUNG
C     RECHTE SEITE = A NABLA(**-2) X(NUE)

         DO KK = 1 , 2
            CALL SOLVE (XNUE,RS2,ZF_FFT,W1,W2,ZX_GAUSS,COEFP,BPTP,
     &           AP,TRIGSI,RZ1I,RZ2I,IFAXI,NFFT,MGAUSS,MT)
            ITYPE = 10000
            KSIZE(1:10) = (/1,0,0,0,0,0,0,0,0,0/)
            CALL HALOEXCH(KSIZE, ITYPE, XNUE)

            DO J = JAH , JEH
               DO I = IAH , IEH
      RS2(I,J) = ALF(I,J) * XNUE(I-1,J) + CLF(I,J) * XNUE(I+1,J)
     &         + APF(I,J) * XNUE(I,J-1) + CPF(I,J) * XNUE(I,J+1)
     &         + BBF(I,J) * XNUE(I,J)
               ENDDO
            ENDDO
         ENDDO

C     FUER DEN EINFACHEN FALL RELAXATIONSKOEFFIZIENTEN = 1
C     KOENNEN 2 SEHR AEHNLICHE TERME ZU FOLGENDEM ERGEBNIS
C     ZUSAMMENGEFASST WERDEN.

         DO J = JAH , JEH
            DO I = IAH , IEH
               RS2(I,J) = (BBH(JOFF+J-JAH) - BBP(JOFF+J-JAH)) * X(I,J)
     &              + ACLDCQ(J) * RS2(I,J) + RSE(I,J)
            ENDDO
         ENDDO

C     BERECHNUNG DER (NUE+1)TEN NAEHERUNG EINER HELMHOLTZGLEICHUNG
C     RECHTE SEITE = RESE(NUE)

         CALL SOLVE (X,RS2,ZF_FFT,W1,W2,ZX_GAUSS,COEFH,BPTH,
     &        AP,TRIGSI,RZ1I,RZ2I,IFAXI,NFFT,MGAUSS,MT)

      ENDDO !NUE = 1 , 20

      RETURN
      END SUBROUTINE CONGRA
