C
C
C@(#) BIBLIOTHEK EM: EUROPA-MODELL / DEUTSCHLAND-MODELL
C@(#) MODUL PHSTOPH.F, V2.12 VOM 4/27/94, EXTRAHIERT AM 4/28/94
C
C**** PHSTOPH  -   FC:UMRECHNUNG VON PHIS (ROT.SYSTEM) AUF PHI(GEOG.SY.)
C**   AUFRUF   :   PHI = PHSTOPH(PHIS,RLAS,POLPHI)
C**   ENTRIES  :   KEINE
C**   ZWECK    :   UMRECHNUNG DES PHIS DES ROTIERTEN SYSTEMS IN PHI
C**                (IN GEOGRAPHISCHEN KOORDINATEN).
C**   VERSIONS-
C**   DATUM    :   13.02.89
C**
C**   EXTERNALS:   KEINE
C**
C**   EINGABE-
C**   PARAMETER:   PHIS:   BREITE IM ROTIERTEN SYSTEM (N>0)
C**                RLAS:   LAENGE IM ROTIERTEN SYSTEM (E>0)
C**                POLPHI: GEOGRAPHISCHE BREITE DES N-POLS DES ROT. SYS.
C**   AUSGABE-
C**   PARAMETER:   GEOGRAPHISCHE BREITE ALS WERT DER FUNCTION
C**
C**   COMMON-
C**   BLOECKE  :   KEINE
C**
C**   METHODE  :   ANWENDUNG DER TRANSFORMATIONSFORMELN ZWISCHEN DEM
C**                ROTIERTEN UND UNROTIERTEM SYSTEM
C**   FEHLERBE-
C**   HANDLUNG :   KEINE
C**   VERFASSER:   D.MAJEWSKI
C
      REAL FUNCTION  PHSTOPH ( PHIS,RLAS,POLPHI )
C
      IMPLICIT NONE
C
C     Dummy Arguments
C
      REAL :: PHIS,RLAS,POLPHI
C
C     Local Variables
C
      REAL :: ZARG, ZCOSPOL, ZPHIS, ZPIR18, ZRLAS, ZRPI18, ZSINPOL
C
      DATA        ZRPI18 , ZPIR18  / 57.2957795 , 0.0174532925 /

      ZSINPOL = SIN(ZPIR18*POLPHI)
      ZCOSPOL = COS(ZPIR18*POLPHI)

      ZPHIS   = ZPIR18*PHIS
      IF(RLAS.GT.180.0) RLAS = RLAS - 360.0
      ZRLAS   = ZPIR18*RLAS

      ZARG    = ZCOSPOL*COS(ZPHIS)*COS(ZRLAS) + ZSINPOL*SIN(ZPHIS)
      PHSTOPH = ZRPI18*ASIN(ZARG)

      RETURN
      END FUNCTION PHSTOPH
