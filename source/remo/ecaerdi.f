C
C     SUBROUTINE ECAERDI
C
C**** *AERDIS* - PARAMETERS FOR THE VERTICAL DISTRIBUTIONS OF AEROSOLS.
C
C     J.F.GELEYN     E.C.M.W.F.     04/11/82.
C
C     PURPOSE.
C     --------
C
C          THIS ROUTINE COMPUTES THE VALUES *PVDAEN* (*N=*S,*L,*U OR *D
C     FOR SEA,LAND,URBAN OR DESERT) OF A SURFACE-NORMALISED VERTICAL
C     DISTRIBUTION OF AEROSOLS' OPTICAL DEPHTS FROM THE ARGUMENT *PETAH*
C     (VERTICAL COORDINATE) AT *KLEVP1* LEVELS. IT ALSO SETS VALUES FOR
C     NON-GEOGRAPHICALLY WEIGHTED TOTAL OPTICAL DEPTHS (AT 0.55 E-06
C     WAVE-LENGTH) *PAEOPN* FOR THE SAME FOUR TYPES AND SIMILEAR OPTICAL
C     DEPHTS DIVIDED BY PRESSURE FOR BACKGROUND WELL-MIXED AEROSOLS
C     OF THREE TYPES *PMNBGA* (*MN*=*TR*,*VO* OR *ST* FOR TROPOSPHERIC,
C     VOLCANIC (STRATOSPHERIC ASHES) OR STRATOSPHERIC (SULFURIC TYPE)).
C     IT FINALLY SET VALUES FOR THE POWER TO BE APPLIED TO A TEMPERATURE
C     RATIO SMALLER THAN ONE IN ORDER TO OBTAIN AN IDEX ONE IN THE
C     STRATOSPHERE AND ZERO IN THE TROPOSPHERE WITH A RELATIVELY SMOOTH
C     TRANSITION (*PTRPT*), AS WELL AS FOR ADSORPTION COEFFICIENTS FOR
C     WATER TO THE THREE TYPE OF TROPOSPHERIC AEROSOLS (*PAEADK*) WITH
C     A MINIMUM VALUE (IN THE WHOLE ATMOSPHERE) FOR THE SUM OF THE
C     PRODUCTS OF *PAEADK* BY THE OPTICAL DEPTHS DIVIDED BY PRESURE
C     THICKNESS: *PAEADM*.
C
C**   INTERFACE.
C     ----------
C
C          *AERDIS* IS CALLED FROM *PHYSC*.
C          THERE ARE SIXTEEN DUMMY ARGUMENTS: *PETAH* IS THE VERTICAL
C     COORDINATE.
C                                             *PVDAEN* (*N=*S,*L,*U OR
C     *D) ARE THE NORMALISED VERTICAL DISTRIBUTIONS.
C                                             *KLEVP1* IS THE NUMBER OF
C     LEVELS.
C                                             *PMNBGA* (*MN*=*TR*,*VO*
C     OR *ST*) ARE THE BACKGROUND OPTICAL DEPTHS DIVIDED BY PRESSURE.
C                                             *PAEOPN* (*N=*S,*L,*U OR
C     *D) ARE THE TOTAL OPTICAL DEPHTS FOR THE VERTICALLY VARYING
C     AEROSOLS.
C                                             *PTRPT* IS THE TEMPERATURE
C     EXPONENT FOR THE STRATOSPHERIC DEFINITION.
C                                             *PAEADK* (1,2,3) AND
C     AND *PAEADM* ARE THE CONSTANTS FOR THE DEFINITION OF THE QUANTITY
C     OF WATER VAPOUR THAT WILL BE ADSORBED TO THE DRY AEROSOLS TO FORM
C     MOIST AEROSOLS.
C
C     METHOD.
C     -------
C
C          STRAIGHTFORWARD, EQUIVALENT HEIGTHS ARE GIVEN IN METERS (8434
C     FOR THE ATMOSPHERE) AND TROPOSPHERIC AND STRATOSPHERIC PRESSURE
C     BOUNDARY VALUES ARE SET AT 101325 AND 19330 *PASCAL.
C
C     EXTERNALS.
C     ----------
C
C          NONE.
C
C     REFERENCE.
C     ----------
C
C          NONE.
C
      SUBROUTINE ECAERDI(PETAH,PVDAES,PVDAEL,PVDAEU,PVDAED,KLEVP1,
     &                   PTRBGA,PVOBGA,PSTBGA,PAEOPS,PAEOPL,PAEOPU,
     &                   PAEOPD,PTRPT,PAEADK,PAEADM)
C
      IMPLICIT NONE
C
C     Dummy Arguments
C
      INTEGER, INTENT(IN)    :: KLEVP1
      REAL,    INTENT(IN)    :: PETAH(KLEVP1)
      REAL,    INTENT(INOUT) :: PVDAES(KLEVP1), PVDAEL(KLEVP1),
     &                          PVDAEU(KLEVP1), PVDAED(KLEVP1)
      REAL,    INTENT(INOUT) :: PAEADK(3)
      REAL,    INTENT(INOUT) :: PTRBGA,PVOBGA,PSTBGA,PAEOPS,PAEOPL,
     &                          PAEOPU,PAEOPD,PTRPT,PAEADM
C
C     Local Variables
C
      INTEGER :: ILEVP1,JK
      REAL    :: ZHSD,ZHSL,ZHSS,ZHSU
C
C     ------------------------------------------------------------------
C
C*         1.     PRELIMINARY SETTING.
C                 ----------- --------
C
      ILEVP1=KLEVP1
C
C     ------------------------------------------------------------------
C
C*         2.     COMPUTATIONS.
C                 -------------
C
      ZHSS=AMAX1(1.,8434./1000.)
      ZHSL=AMAX1(1.,8434./1000.)
      ZHSU=AMAX1(1.,8434./1000.)
      ZHSD=AMAX1(1.,8434./3000.)
      PVDAES(1)=0.
      PVDAEL(1)=0.
      PVDAEU(1)=0.
      PVDAED(1)=0.
      IF(PETAH(1).NE.0.) THEN
         PVDAES(1)=PETAH(1)**ZHSS
         PVDAEL(1)=PETAH(1)**ZHSL
         PVDAEU(1)=PETAH(1)**ZHSU
         PVDAED(1)=PETAH(1)**ZHSD
      ENDIF
      DO JK=2,ILEVP1
         PVDAES(JK)=PETAH(JK)**ZHSS
         PVDAEL(JK)=PETAH(JK)**ZHSL
         PVDAEU(JK)=PETAH(JK)**ZHSU
         PVDAED(JK)=PETAH(JK)**ZHSD
      ENDDO
      PTRBGA=0.03/(101325.-19330.)
      PVOBGA=0.007/19330.
C     PVOBGA=1.E-45
      PSTBGA=0.045/19330.
C     PSTBGA=1.E-45
      PAEOPS=0.05
      PAEOPL=0.2
      PAEOPU=0.1
      PAEOPD=1.9
      PTRPT=30.
      PAEADK(1)=+.3876E-03
      PAEADK(2)=+.6693E-02
      PAEADK(3)=+.8563E-03
      PAEADM=2.6E-10
C
C     ------------------------------------------------------------------
C
C*         3.     RETURN.
C                 -------
C
      RETURN
      END
