C
C     SUBROUTINE DATAUS
C
C**** DATAUS   -   UP:EIN FELD IN 32-BIT-IEEE UMWANDELN UND AUSGEBEN
C**   AUFRUF   :   CALL DATAUS(IPDB , IGDB , REF  , ZAK, ZBK, SFELD,
C**                            IEJEX, KEMAX, NUNIT)
C**   ENTRIES  :   KEINE
C**   ZWECK    :   EIN FELD IN 32-BIT-IEEE UMWANDELN UND AUF EINHEIT
C**               'NUNIT' AUSGEBEN
C**   VERSIONS-
C**   DATUM    :   27.03.01
C**
C**   EXTERNALS:   KEINE
C**
C**   EINGABE-
C**   PARAMETER:   NUNIT: UNIT-NUMMER DER AUSGABE-DATEI
C**                SFELD: SKALIERTES FELD
C**                IEJEX: DIMENSIONIERUNG SFELD
C**                IPDB : PRODUCT-DEFINITION BLOCK DES GRIB
C**                IGDB : GRID-DEFINITION BLOCK DES GRIB
C**                REF  : REFERENZWERT DES GRIB (0.)
C**                ZAK  : VERTIKALKOORDINATEN PARAMETER (AK)
C**                ZBK  : VERTIKALKOORDINATEN PARAMETER (BK)
C**   AUSGABE-
C**   PARAMETER:   KEINE
C**
C**   COMMON-
C**   BLOECKE  :   KEINE
C**
C**   FEHLERBE-
C**   HANDLUNG :   KEINE
C**   VERFASSER:   R.PODZUN
C
      SUBROUTINE DATAUS(IPDB,IGDB,REF,ZAK,ZBK,SFELD,IEJEX,KEMAX,NUNIT)
C
      IMPLICIT NONE
C
C     Dummy Arguments
C
      INTEGER,          INTENT(IN) :: IEJEX,KEMAX,NUNIT
      INTEGER,          INTENT(IN) :: IPDB(37),IGDB(22)
      DOUBLE PRECISION, INTENT(IN) :: REF,ZAK(KEMAX),
     &                                ZBK(KEMAX),SFELD(IEJEX)
C
C     Local Variables
C
      REAL    :: RREF
      REAL    :: RZAK(KEMAX), RZBK(KEMAX), RSFELD(IEJEX)
      INTEGER :: I,J,K,IJ
C
      RREF=REAL(REF)
C
      DO K=1,KEMAX
         RZAK(K)=REAL(ZAK(K))
         RZBK(K)=REAL(ZBK(K))
      ENDDO
C
      DO IJ=1,IEJEX
         RSFELD(IJ)=REAL(SFELD(IJ))
      ENDDO
C
      WRITE(NUNIT) IPDB,(IGDB(I),I=1,18),RREF,
     &            (IGDB(J),J=20,22),RZAK,RZBK
      WRITE(NUNIT) RSFELD
C
      RETURN
      END SUBROUTINE DATAUS
