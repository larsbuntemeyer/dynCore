C
C     SUBROUTINE DATCHK
C
C**** DATCHK   -   UP:PRUEFEN, OB DER TERMIN DER GRIB-CODE-FELDER MIT
C****                 DEM ANGEFORDERTEN TERMIN UEBEREINSTIMMT UND OB
C****                 DIE VERSIONSNUMMERN IDENTISCH SIND.
C**   AUFRUF   :   CALL DATCHK(YTYP, NZEIT, ISTAT) IN UP'S *ADPREP* UND
C**                                                        *RDPREP*
C**   ENTRIES  :   KEINE
C**   ZWECK    :   TERMIN UND VERSIONSNUMMER DER GRIB-CODE-FELDER UEBER-
C**                PRUEFEN, OB SIE MIT DEN ANGEFORDERTEN WERTEN UEBER-
C**                EINSTIMMEN
C**   VERSIONS-
C**   DATUM    :   08.02.89
C**
C**   EXTERNALS:   PRCV, PRIV, DATUTC
C**
C**   EINGABE-
C**   PARAMETER:   YTYP:  DATENTYP  ('A' ODER 'R')
C**                NZEIT: ZEITSCHRITT
C**   AUSGABE-
C**   PARAMETER:   ISTAT: STATUSPARAMETER; ISTAT=0: ALLES O.K.
C**
C**   COMMON-
C**   BLOECKE  :   GRIB, ORG, CORG, COMDYN
C**
C**   METHODE  :   AUS DEM PDB DIE INFORMATIONEN HOLEN UND MIT DEN ANGE-
C**                FORDERTEN WERTEN VERGLEICHEN
C**   FEHLERBE-
C**   HANDLUNG :   FEHLERSCHLUESSEL SETZEN
C**   VERFASSER:   D.MAJEWSKI
C
      SUBROUTINE DATCHK(YTYP, NZEIT, ISTAT)
C
      IMPLICIT NONE
C
      INCLUDE "grib.h"
      INCLUDE "org.h"
      INCLUDE "corg.h"
      INCLUDE "comdyn.h"
C
C     Dummy Arguments
C
      CHARACTER, INTENT(IN)  :: YTYP*(*)
      INTEGER,   INTENT(IN)  :: NZEIT
      INTEGER,   INTENT(OUT) :: ISTAT
C
C     Local Variables
C
      CHARACTER  ::  YDATGB*10, YAKDRD1*10, YAKDRD2*21
      REAL       ::  AKHHRD
      INTEGER    ::  NHGB,NDGB,MMGB,JJGB,NAKTARD
C
C     DATUM,TERMIN UND VERSIONSNUMMER AUS PDB
      JJGB    = IPDB(11)
      MMGB    = IPDB(12)
      NDGB    = IPDB(13)
      NHGB    = IPDB(14)
      ISTAT   = 0
C
C     DIE KONSTANTEN BODENFELDER HABEN D=000000 UND BRAUCHEN NICHT MIT
C     DEM AKTUELLEN DATUM UEBEREINZUSTIMMEN
      IF(JJGB+MMGB+NDGB.EQ.0) RETURN
C
      WRITE(YDATGB,'(I4,3I2.2)') JJGB, MMGB, NDGB, NHGB
C
C     WENN YTYP = 'A': ANFANGSDATEN
C
      IF (YTYP.EQ.'A') THEN
C
C     ANALYSEN ALS ANFANGSDATEN
          IF (YDATGB.NE.YAKDAT1) THEN
              PRINT *,'SUBROUTINE DATCHK:'
              PRINT *,'DATUM MODELL =', YAKDAT1
              PRINT *,'DATUM A-DATEI=', YDATGB
              ISTAT = 1
              RETURN
           ENDIF
      ENDIF
C
C     WENN YTYP = 'F': FORTSETZUNGSDATEI
C
      IF (YTYP.EQ.'F') THEN
C
C     ANALYSEN ALS ANFANGSDATEN
          IF (YDATGB.NE.YAKDAT1) THEN
              PRINT *,'SUBROUTINE DATCHK:'
              PRINT *,'DATUM MODELL =', YAKDAT1
              PRINT *,'DATUM F-DATEI=', YDATGB
              ISTAT = 2
              RETURN
           ENDIF
      ENDIF
C
C     WENN YTYP = 'G': FORTSETZUNGSDATEI
C
      IF (YTYP.EQ.'G') THEN
C
C     ANALYSEN ALS ANFANGSDATEN
          IF (YDATGB.NE.YAKDAT1) THEN
              PRINT *,'SUBROUTINE DATCHK:'
              PRINT *,'DATUM MODELL =', YAKDAT1
              PRINT *,'DATUM F-DATEI=', YDATGB
              ISTAT = 3
              RETURN
           ENDIF
      ENDIF
C
C     WENN YTYP = 'R': RANDDATEN
C
      IF (YTYP.EQ.'R') THEN
C
C     ANALYSEN ALS RANDDATEN
C
          CALL DATUTC(NZEIT, YADAT, DT, YAKDRD1, YAKDRD2,
     &                NAKTARD, AKHHRD)
          IF (YDATGB.NE.YAKDRD1) THEN
              PRINT *,'SUBROUTINE DATCHK:'
              PRINT *,'DATUM MODELL =', YAKDRD1
              PRINT *,'DATUM R-DATEI=', YDATGB
              ISTAT = 4
              RETURN
          ENDIF
      ENDIF
      RETURN
      END SUBROUTINE DATCHK
