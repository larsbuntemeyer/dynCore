C
C
C**** PREPBOD  -   UP:AUFBEREITUNG DER MONATSWEISE VORHANDENEN AEROSOLFELDER
C**
C**   AUFRUF   :   CALL PREPAER IN UP *EC4ORG*
C**
C**   ENTRIES  :   KEINE
C**
C**   ZWECK    :   AUFBEREITUNG DER MONATSWEISE VORHANDENEN AEROSOLFELDER
C**                SO4ALL
C**
C**   VERSIONS-
C**   DATUM    :   04.04.06
C**                2007
C**
C**   EXTERNALS:   KEINE
C**
C**   EINGABE-
C**   PARAMETER:   ZSO4ALL, ZSO4NAT
C**
C**   AUSGABE-
C**   PARAMETER:   SO4ALL, SO4NAT
C**
C**   COMMON-
C**   BLOECKE  :   ORG, CORG, COMDYN, COMCON
C**
C**   METHODE  :   LINEARE ZEITLICHE INTERPOLATION ZWISCHEN DEN
C**                AUF MONATSBASIS VORHANDENEN OZONFELDERN
C**   FEHLERBE-
C**   HANDLUNG :   KEINE
C**   VERFASSER:   R.PODZUN
C
      SUBROUTINE PREPAER(SO4ALL, ZSO4ALL, SO4NAT, ZSO4NAT)
C
      IMPLICIT NONE
C
      INCLUDE "parorg.h"
      INCLUDE "org.h"
      INCLUDE "corg.h"
C not used      INCLUDE "COMCON"
      INCLUDE "comdyn.h"
C
C     Dummy Arguments
C
      REAL, INTENT(IN)    :: ZSO4ALL(IEJE,KE,14), ZSO4NAT(IEJE,KE,14)
      REAL, INTENT(INOUT) :: SO4ALL(IEJE,KE)    , SO4NAT(IEJE,KE)
C
C     Local Variables
C
      INTEGER :: MONDAY(14), MONATB(14)
      INTEGER :: I100, I4, I400, ID, IDAYL, IH, IJ, IM, IMM1, IMM1SEC, 
     &           IMP1, IMP1SEC, IMSECA, IMSECB, ISCHLT, ISEC, IY, K, M
      REAL    :: ZMONSECA, ZMONSECB, ZSECA, ZSECB, ZW1, ZW2
C
      DATA  MONATB / 31, 31, 28, 31, 30, 31, 30 ,
     &               31, 31, 30, 31, 30, 31, 31 /
C
      IDAYL=86400
C
      CALL DATUTC(NZT, YADAT, DT, YAKDAT1, YAKDAT2, NAKJATA,
     &            AKHH)
C
C     AKTUELLES DATUM EINLESEN
C
      READ(YAKDAT1,'(I4,3I2)') IY,IM,ID,IH
C
C     DA 14 WERTE VORLIEGEN DEZ VORJAHR=1, JAN FOLGEJAHR=14
C     => JAN-DEZ=2-13
C     DAHER MONATSZAHL UM 1 ERHOEHEN
C
      IM=IM+1
      IMM1=IM-1
      IMP1=IM+1
C
      DO M=1,14
         MONDAY(M)=MONATB(M)
      ENDDO
C
C     SCHALTJAHR BESTIMMEN
C
      ISCHLT=0
      I4=MOD(IY,4)
      I100=MOD(IY,100)
      I400=MOD(IY,400)
      IF (I4.EQ.0) ISCHLT=1
      IF (I100.EQ.0) ISCHLT=0
      IF (I400.EQ.0) ISCHLT=1
      MONDAY(3)=MONDAY(3)+ISCHLT
C
C     ZAHL DER SEKUNDEN DER ZWEITEN MONATSHAELFTE DES VORMONATS
C
      IMM1SEC=(MOD(MONDAY(IMM1),2)*IDAYL)+((MONDAY(IMM1)/2)*IDAYL)
C
C     ZAHL DER SEKUNDEN DER ERSTEN MONATSHAELFTE DES AKTUELLEN MONATS
C
      IMSECA=(MONDAY(IM)/2)*IDAYL
C
C     ZAHL DER SEKUNDEN DES GESAMTEN INTERVALLS
C     (16.(VORMONAT) - 15.[INKL.] (AKTUELLER MONAT))
C
      ZMONSECA=FLOAT(IMM1SEC+IMSECA)
C
C     ZAHL DER SEKUNDEN DER ZWEITEN MONATSHAELFTE DES AKTUELLEN MONATS
C
      IMSECB=(MOD(MONDAY(IM),2)*IDAYL)+((MONDAY(IM)/2)*IDAYL)
C
C     ZAHL DER SEKUNDEN DER ERSTEN MONATSHAELFTE DES FOLGEMONATS
C
      IMP1SEC=(MONDAY(IMP1)/2)*IDAYL
C
C     ZAHL DER SEKUNDEN DES GESAMTEN INTERVALLS
C     (16.(AKTUELLER MONAT) - 15.[INKL.] (FOLGEMONAT))
C
      ZMONSECB=FLOAT(IMP1SEC+IMSECB)
C
C     ZAHL DER SEKUNDEN DES AKTUELLEN DATUMS BERECHEN
C
      ISEC=(ID-1)*IDAYL+IH*3600
C
C     ZAHL DER SEKUNDEN DES AKTUELLEN DATUMS IM JEWEILIGEN INTERVALL
C
      ZSECA=FLOAT(ISEC+IMM1SEC)
      ZSECB=FLOAT(ISEC-IMSECA)
C
C     BERECHNUNG DER BODENFELDER
C
C     ERSTE MOANTASHAELFTE
C
      IF (ID.LE.15) THEN
C
         ZW1=ZSECA/(ZMONSECA)
         ZW2=1.-ZW1
         DO K=1,KE
            DO IJ=1,IEJE
               SO4ALL(IJ,K)=ZW1*ZSO4ALL(IJ,K,IM)+ZW2*ZSO4ALL(IJ,K,IMM1)
               SO4NAT(IJ,K)=ZW1*ZSO4NAT(IJ,K,IM)+ZW2*ZSO4NAT(IJ,K,IMM1)
            ENDDO
         ENDDO
C
C     ZWEITE MOANTASHAELFTE
C
      ELSE
C
         ZW2=ZSECB/(ZMONSECB)
         ZW1=1.-ZW2
         DO K=1,KE
            DO IJ=1,IEJE
               SO4ALL(IJ,K)=ZW1*ZSO4ALL(IJ,K,IM)+ZW2*ZSO4ALL(IJ,K,IMP1)
               SO4NAT(IJ,K)=ZW1*ZSO4NAT(IJ,K,IM)+ZW2*ZSO4NAT(IJ,K,IMP1)
            ENDDO
         ENDDO
C
      ENDIF
C
      RETURN
      END SUBROUTINE PREPAER
