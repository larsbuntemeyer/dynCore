      SUBROUTINE RAIN_ADV
     &           (PADVE, GEOM1, DT, APHM1, G,
     &            WLK, WLS, WCP, PQDADV, PTADV, APM1, R, RD,
     &            QD, T, NA, PI, ACLC, NZT, U, V, ACPHIR, CPHI,
     &            JPTLUCU1, JPTLUCU2, TLUCUA, TLUCUAW,
     &            PQTE, PTTE)
C
C**   AUFRUF   :   CALL RAIN_ADV IN PHYEC.F
C**   ZWECK    :   SUBROUTINE FOR RAIN ADVECTION
C**
C**
C**   DATUM    :   2010
C**
C**   FEHLERBE-
C**   HANDLUNG :   ---
C**   VERFASSER:   J-P. PIETIKÃ„INEN

      IMPLICIT NONE ! -> TYPOS NONE :)

      INCLUDE "parorg.h"
C
      INCLUDE "haloexchorg"

C-----------------------------------------------------------------------
C
C     INPUT/OUTPUT FIELDS:
C
      REAL, INTENT(INOUT) :: PADVE(IEJE,KE,4)    ! ADVECTION FIELD, INDEXES:
C                                                ! 1) CONVECTIVE RAIN
C                                                ! 2) CONVECTIVE SNOW
C                                                ! 3) STRATIFORM RAIN
C                                                ! 4) STRATIFORM SNOW
      REAL, INTENT(IN)    :: GEOM1(IEJE,KE)      ! GEOPOTENTIAL HIGHT
      REAL, INTENT(IN)    :: DT                  ! TIME STEP
      REAL, INTENT(IN)    :: APHM1(IEJE,KE1)     ! PRESSURE AT HALF LEVELS
      REAL, INTENT(IN)    :: G                   ! GRAVITY ACCELERATION
      REAL, INTENT(IN)    :: WLK                 ! LATENT HEAT FOR VAPORISATION
      REAL, INTENT(IN)    :: WLS                 ! LATENT HEAT FOR SUBLIMATION
      REAL, INTENT(IN)    :: WCP                 ! SPECIFIC HEAT AT CONSTANT
C                                                  PRESSURE (DRY AIR)
      REAL, INTENT(IN)    :: PQDADV(IEJE,KE)     ! UPDATED SPECIFIC HUMIDITY
      REAL, INTENT(IN)    :: PTADV(IEJE,KE)      ! UPDATED TEMPERATURE
      REAL, INTENT(IN)    :: APM1(IEJE,KE)       ! PRESSURE AT FULL LEVELS
      REAL, INTENT(IN)    :: R                   ! GAS CONSTANT FOR DRY AIR
      REAL, INTENT(IN)    :: RD                  ! GAS CONSTANT FOR WATER VAPOUR
      REAL, INTENT(IN)    :: QD(IEJE,KE,3)       ! SPECIFIC HUMIDITY
      REAL, INTENT(IN)    :: T(IEJE,KE,3)        ! TEMPERATURE
      INTEGER, INTENT(IN) :: NA                  ! TIME STEP INDEX
      REAL, INTENT(IN)    :: PI                  ! PI
      REAL, INTENT(IN)    :: ACLC(IEJE,KE)       ! CLOUD COVER
      INTEGER, INTENT(IN) :: NZT                 ! STEP NUMBER
      REAL, INTENT(IN)    :: U(IEJE,KE,3)        ! U-WIND
      REAL, INTENT(IN)    :: V(IEJE,KE,3)        ! V-WIND
      REAL, INTENT(IN)    :: ACPHIR(JE,2)        ! ACPHIR
      REAL, INTENT(IN)    :: CPHI(JE,2)          ! CPHI
      INTEGER, INTENT(IN) :: JPTLUCU1, JPTLUCU2  ! LOOKUPTABLE INDEXES
      REAL, INTENT(IN)    ::
     &     TLUCUA(JPTLUCU1:JPTLUCU2),            ! LOOKUPTABLES
     &     TLUCUAW(JPTLUCU1:JPTLUCU2)            ! LOOKUPTABLES
      REAL, INTENT(OUT)   :: PQTE(IEJE,KE)       ! HUMIDITY TENDENCY
      REAL, INTENT(OUT)   :: PTTE(IEJE,KE)       ! TEMPERATURE TENDENCY
C     PARAMETERS
      REAL, PARAMETER     :: A       = 4.16
      REAL, PARAMETER     :: B       = 0.084
      REAL, PARAMETER     :: RHOH2O  = 1000.0    ! KG/M3
CKS      REAL, PARAMETER     :: RHOSNOW = 100.0     ! KG/M3
      REAL, PARAMETER     :: CQTMIN  = 1.E-12    ! TOTAL WATER MINIMUM
      REAL, PARAMETER     :: WPV     = 1869.46
      REAL, PARAMETER     :: TMELT   = 273.16    ! MELTING TEMPERATURE
C                                                  OF ICE/SNOW
      REAL, PARAMETER     :: CVTFALL = 3.29
      REAL, PARAMETER     :: CRHOSNO = 100.0
      REAL, PARAMETER     :: CN0S    = 3.E6
      REAL, PARAMETER     :: CMICOV  = 0.05      ! MINIMUM CLOUD COVER
      REAL, PARAMETER     :: CONFRA  = 0.05      ! FRACTION OF PRECIPITATIN
C                                                  GRID BOX FOR CONVECTION
      REAL, PARAMETER     :: PRELOLI = 1.E-10    ! MINIMUM AMOUNT OF
C                                                  PRECIPITATION IN KG/(M2S)
C                                                  TO ACTIVATE ADVECTION
      INTEGER, PARAMETER  :: NADV    = 4         ! NUMBER OF ADVECTED SPECIES

C     LOCAL FIELDS
      REAL :: ZPREVELO, ZFAC1, ZFALLDIS
CKS   REAL :: ZFAC2
      REAL :: ZSNOWVELO, ZDP, ZCONS, ZCONS1, ZCONS2, ZRCP,
     &     ZLSDCP, ZTDIF, ZEPSEC, ZXSEC, ZSFLC, ZCLCSTAR, ZDPG, ZRHO, 
     &     ZESI, ZQSI, VTMPC1, ZSUSATI, ZB1, ZB2, ZCOEFF, ZXSP1,
     &     ZCLAMBS, ZCFAC4C, ZZEPS, ZSUBC, ZESW, ZESAT, ZQSW, ZSUSATW, 
     &     ZDV, ZAST, ZBST, ZZEPR, ZEVPC, ZPRETOT, ZSMLT, 
     &     ZZT2, ZCLCPRE, ZLVDCP, ZSNMLTC, ZQRHO, ZPREDEL, ZPRESUM,
     &     ZRFLC, ZRFLS, ZSFLS, ZSNMLTS, ZEVPS, ZSUBS, ZLIEVA, ZLIMEL,
     &     ISTOP

      REAL :: ZGEOM1(IEJE,KE-1), ZPREFIELD(IEJE,KE,4)

      INTEGER K, IV(KE-1), IVV(KE-1), KI, IJ
      INTEGER IT, IZT2, IOFF, IEOFF, JOFF, JEOFF, ION, IEON
      INTEGER JON, JEON, IXY, IL, IPREV(4)

      LOGICAL LO

      INTEGER :: I, IA, II, IJA, IJI, J, JA, JI
CKS
C     FOR HALO EXCHANGE
      INTEGER :: KSIZE(10), ITYPE

C-----------------------------------------------------------------------
C
C     INITIALIZATION
      DO K = 1,KE-1
         IV(K) = K
      ENDDO
C
C     CONVERSION FACTOR FOR RAIN KG/(M2S) -> MM
      ZFAC1=DT/RHOH2O*1000.0
C     CONVERSION FACTOR FOR SNOW KG/(M2S) -> MM
CKS      ZFAC2=DT/RHOSNOW*1000.0
C
      ZCONS1=WCP*(WPV/WCP-1.0)
      ZCONS2=1./(DT*G)
      ZEPSEC=1.E-12
      ZXSEC=1.0-ZEPSEC
      VTMPC1=RD/R-1.0
C
C     INITIALIZE THE ADVECTED PRECIPITATION FIELD TO ZERO
      ZPREFIELD(:,:,:) = 0.0
C
C     CALCULATE THE HALFLEVEL GEOPOTENTIAL LEVELS
      ZGEOM1(:,:) = 0.0
      DO K=1,KE-1
         DO IJ=1,IEJE
            ZGEOM1(IJ,K) = (GEOM1(IJ,K) + GEOM1(IJ,K+1))/2.0
         ENDDO
      ENDDO
C
C     CALCULATE FOR EACH KE-1 LEVEL THE IEJE FIELD TO CHECK IF THERE IS
C     A FLUX OF PRECIPITATION (RAIN OR SNOW)
      KLOOP: DO K = 1, KE-1
      IEJELOOP: DO IJ=1,IEJE
C
      IF(ALL(PADVE(IJ,K,:) == 0.0)) CYCLE IEJELOOP
C
C     INITIALIZATION
      IPREV(:) = 0
      ZLIEVA   = 0.0
      ZLIMEL   = 0.0
      ZCLCPRE  = MAX(CMICOV,MAXVAL(ACLC(IJ,:)))
      ZRFLC    = 0.0
      ZSFLC    = 0.0
      ZRFLS    = 0.0
      ZSFLS    = 0.0
C
C     IF THE AMOUNT OF SNOW IS UNDER THE ADVECTION LIMIT -> MELTING
      IF(PADVE(IJ,K,2) .GT. 0.0 .AND. PADVE(IJ,K,2) .LT. PRELOLI)THEN
         PADVE(IJ,K,1) = PADVE(IJ,K,1) + PADVE(IJ,K,2)
         ZLIMEL        = ZLIMEL + PADVE(IJ,K,2)
         PADVE(IJ,K,2) = 0.0
      ENDIF
      IF (PADVE(IJ,K,4) .GT. 0.0 .AND. PADVE(IJ,K,4).LT.
     &        PRELOLI) THEN
         PADVE(IJ,K,3) = PADVE(IJ,K,3) + PADVE(IJ,K,4)
         ZLIMEL        = ZLIMEL + PADVE(IJ,K,4)
         PADVE(IJ,K,4) = 0.0
      ENDIF
C     IF THE AMOUNT OF RAIN IS UNDER THE ADVECTION LIMIT -> EVAPORATION
      IF(PADVE(IJ,K,1) .GT. 0.0 .AND. PADVE(IJ,K,1) .LT. PRELOLI)THEN
         ZLIEVA        = ZLIEVA + PADVE(IJ,K,1)
         PADVE(IJ,K,1) = 0.0
      ENDIF
      IF (PADVE(IJ,K,3) .GT. 0.0 .AND. PADVE(IJ,K,3) .LT.
     &        PRELOLI) THEN
         ZLIEVA        = ZLIEVA + PADVE(IJ,K,3)
         PADVE(IJ,K,3) = 0.0
      ENDIF
C
      IF(PADVE(IJ,K,1) .GE. PRELOLI) THEN
C        CONVECTIVE RAIN
         IVV(:) = IV(:)
C        PRECIPITATION VELOCITY M/S
         ZPREVELO=MAX(1.0,MIN(10.0,A*(PADVE(IJ,K,1)*ZFAC1/DT*3600)**B))
C        DISTANCE OF VERTICAL MOTION FOR RAIN PER TIME STEP
         ZFALLDIS=ZPREVELO*DT
C        CALCULATE THE LEVEL WHICH PRECIPITATION REACHES AND
C        FORCE THE PRECIPITATION TO FALL AT MINIMUM 1 LEVEL
CBE   CHANGE IVV from 0 to 999
         WHERE(ZGEOM1(IJ,:)/G >= GEOM1(IJ,K)/G-ZFALLDIS) IVV(:)=999
         IPREV(1) = MAX(MINVAL(IVV(:)),K+1)
         IF(ALL(IVV(:) == 999)) IPREV(1) = KE ! TO THE GROUND
      ENDIF
      IF (PADVE(IJ,K,2) .GE. PRELOLI) THEN
C        CONVECTIVE SNOW
         IVV(:) = IV(:)
C        SNOW TERMINAL VELOCITY M/S
         ZSNOWVELO=1.0
C        DISTANCE OF VERTICAL MOTION FOR RAIN PER TIME STEP
         ZFALLDIS=ZSNOWVELO*DT
C        CALCULATE THE LEVEL WHICH PRECIPITATION REACHES AND
C        FORCE THE PRECIPITATION TO FALL AT MINIMUM 1 LEVEL
CBE   CHANGE IVV from 0 to 999
         WHERE(ZGEOM1(IJ,:)/G >= GEOM1(IJ,K)/G-ZFALLDIS) IVV(:)=999
         IPREV(2) = MAX(MINVAL(IVV(:)),K+1)
         IF(ALL(IVV(:) == 999)) IPREV(2) = KE ! TO THE GROUND
      ENDIF
      IF (PADVE(IJ,K,3) .GE. PRELOLI) THEN
C        STRATIFORM RAIN
         IVV(:) = IV(:)
C        PRECIPITATION VELOCITY M/S
         ZPREVELO=MAX(1.0,MIN(10.0,A*(PADVE(IJ,K,3)*ZFAC1/DT*3600)**B))
C        DISTANCE OF VERTICAL MOTION FOR RAIN PER TIME STEP
         ZFALLDIS=ZPREVELO*DT
C        CALCULATE THE LEVEL WHICH PRECIPITATION REACHES AND
C        FORCE THE PRECIPITATION TO FALL AT MINIMUM 1 LEVEL
CBE   CHANGE IVV from 0 to 999
         WHERE(ZGEOM1(IJ,:)/G >= GEOM1(IJ,K)/G-ZFALLDIS) IVV(:)=999
         IPREV(3) = MAX(MINVAL(IVV(:)),K+1)
         IF(ALL(IVV(:) == 999)) IPREV(3) = KE ! TO THE GROUND
      ENDIF
      IF (PADVE(IJ,K,4) .GE. PRELOLI) THEN
C        STRATIFORM SNOW
         IVV(:) = IV(:)
C        SNOW TERMINAL VELOCITY M/S
         ZSNOWVELO=1.0
C        DISTANCE OF VERTICAL MOTION FOR RAIN PER TIME STEP
         ZFALLDIS=ZSNOWVELO*DT
C        CALCULATE THE LEVEL WHICH PRECIPITATION REACHES AND
C        FORCE THE PRECIPITATION TO FALL AT MINIMUM 1 LEVEL
CBE   CHANGE IVV from 0 to 999
         WHERE(ZGEOM1(IJ,:)/G >= GEOM1(IJ,K)/G-ZFALLDIS) IVV(:)=999
         IPREV(4) = MAX(MINVAL(IVV(:)),K+1)
         IF(ALL(IVV(:) == 999)) IPREV(4) = KE ! TO THE GROUND
      ENDIF
C
      IF(MAXVAL(IPREV(:)) == 0) CYCLE IEJELOOP
C
      IF(IPREV(1) /= 0) ZRFLC = PADVE(IJ,K,1) ! INITIAL VALUE FOR
C                                               CONVECTIVE RAIN
      IF(IPREV(2) /= 0) ZSFLC = PADVE(IJ,K,2) ! INITIAL VALUE FOR
C                                               CONVECTIVE SNOW
      IF(IPREV(3) /= 0) ZRFLS = PADVE(IJ,K,3) ! INITIAL VALUE FOR
C                                               STRATIFORM RAIN
      IF(IPREV(4) /= 0) ZSFLS = PADVE(IJ,K,4) ! INITIAL VALUE FOR
C                                               STRATIFORM SNOW
C
C     ADVECTED FIELDS ARE TREATED SEPARATELY AND THUS THE VALUES IN PADVE
C     MATRIX MUST BE RESET
      IF(IPREV(1) /= 0) PADVE(IJ,K,1) = 0.0
      IF(IPREV(2) /= 0) PADVE(IJ,K,2) = 0.0
      IF(IPREV(3) /= 0) PADVE(IJ,K,3) = 0.0
      IF(IPREV(4) /= 0) PADVE(IJ,K,4) = 0.0
C
C     DO THE VERTICAL LOOP FOR PRECIPITATION (FROM LEVEL K TO MAX(IPREV))
CBE   CHANGE KI = 1 to KI = K+1
      DO KI = K+1,MAXVAL(IPREV(:))
C
         ZEVPC   = 0.0          ! INITIAL VALUE FOR CONVECTIVE EVAPORATION
         ZEVPS   = 0.0          ! INITIAL VALUE FOR STRATIFORM EVAPORATION
         ZSUBC   = 0.0          ! INITIAL VALUE FOR CONVECTIVE SUBLIMATION
         ZSUBS   = 0.0          ! INITIAL VALUE FOR STRATIFORM SUBLIMATION
         ZSNMLTC = 0.0          ! INITIAL VALUE FOR CONVECTIVE MELTING
         ZSNMLTS = 0.0          ! INITIAL VALUE FOR STRATIFORM MELTING
         ZDP=APHM1(IJ,KI+1)-APHM1(IJ,KI) ! PRESSURE CHANGE
         ZRHO=APM1(IJ,KI)/(R*PTADV(IJ,KI)) ! DENSITY
C        FOR CONVECTIVE SNOW
         IF(IPREV(2) /= 0 .AND. KI <= IPREV(2)) THEN
C     *         MELTING OF SNOW
C
            ZRCP=1./(WCP+ZCONS1*PQDADV(IJ,KI))
            ZLSDCP=WLS*ZRCP
            ZLVDCP=WLK*ZRCP
            ZCONS=ZCONS2*ZDP/(ZLSDCP-ZLVDCP)
            ZTDIF=MAX(0.0,T(IJ,KI,NA)-TMELT)
            ZSNMLTC=MIN(ZXSEC*ZSFLC,ZCONS*ZTDIF)
            ZSFLC=ZSFLC-ZSNMLTC
            ZRFLC=ZRFLC+ZSNMLTC
C           SUBLIMATION OF SNOW (LIN ET. AL, 1983)
            IF (ZSFLC .GT. 0.0) THEN
               ZCLCSTAR=CONFRA
               ZDPG=ZDP/G
               ZQRHO=1.3/ZRHO
               IT=NINT(T(IJ,KI,NA)*1000.)
CSP            HIER IST IN ECHAM EINE ABFRAGE AUF LOOKUPOVERFLOW, WENN IT
CSP            JPTLUCU1 ODER > LPTLUCU2
               IT=MAX(MIN(IT, JPTLUCU2),JPTLUCU1)
               ZESI=TLUCUA(IT)/APM1(IJ,KI)
               ZESI=MIN(ZESI, 0.5)
               ZQSI=ZESI/(1.-VTMPC1*ZESI) !*VTMPC1=RV/RD-1.
               ZSUSATI=MIN(QD(IJ,KI,NA)/ZQSI-1.0,0.0)
               ZB1=ZLSDCP**2/(2.43E-2*RD*(T(IJ,KI,NA)**2))
               ZB2=1./(ZRHO*ZQSI*0.211E-4)
               ZCOEFF=3.E6*2.*PI*ZSUSATI/(ZRHO*(ZB1+ZB2))
               IF (ZSFLC .GT. CQTMIN) THEN
                  ZXSP1=(ZSFLC/(CONFRA*CVTFALL))**(1./1.16)
                  ZCLAMBS=(ZXSP1/(PI*CRHOSNO*CN0S))**0.25
                  ZCFAC4C=0.78*ZCLAMBS**2+232.19*ZQRHO**0.25
     &                 *ZCLAMBS**2.625
                  ZZEPS=MAX(-ZXSEC*ZSFLC/CONFRA,ZCOEFF*ZCFAC4C*ZDPG)
                  ZSUBC=-ZZEPS/ZDPG*DT*ZCLCSTAR
                  ZSUBC=MIN(ZSUBC,MAX(ZXSEC*(ZQSI-QD(IJ,KI,NA)),0.0))
                  ZSUBC=MAX(ZSUBC,0.0)
               ENDIF
            ENDIF               ! ZCLCPRE .GT. 0.0
         ENDIF                  ! IPREV(2) /= 0 .AND. KI <= IPREV(2) 
C        FOR CONVECTIVE RAIN
         ISTOP = 0              ! INIT VALUE FOR PRECIP. STOP VALUE
         IF(IPREV(1) /= 0) THEN
            ISTOP=IPREV(1)
         ELSE
            ISTOP=IPREV(2)
         ENDIF
         IF(ZRFLC .GT. 0.0 .AND. KI <= ISTOP) THEN
C           EVAPORATION OF RAIN  (ROTSTAYN, 1997)
C
            ZCLCSTAR=CONFRA
            ZDPG=ZDP/G
            IT=NINT(T(IJ,KI,NA)*1000.)
CSP         HIER IST IN ECHAM EINE ABFRAGE AUF LOOKUPOVERFLOW, WENN
CSP         IT < JPTLUCU1 ODER > LPTLUCU2
            IT=MAX(MIN(IT,JPTLUCU2),JPTLUCU1)
            ZESW=TLUCUAW(IT)/APM1(IJ,KI)
            ZESAT=ZESW*APM1(IJ,KI)*RD/R
            ZESW=MIN(ZESW,0.5)
            ZQSW=ZESW/(1.-VTMPC1*ZESW)
            ZSUSATW=MIN(QD(IJ,KI,NA)/ZQSW-1.0,0.0)
            ZDV=2.21/APM1(IJ,KI)
            ZAST=WLK*(WLK/(RD*T(IJ,KI,NA))-1.0)/(0.024*T(IJ,KI,NA))
            ZBST=RD*T(IJ,KI,NA)/(ZDV*ZESAT)
            ZZEPR=870.*ZSUSATW*(ZRFLC/CONFRA)**0.61
     &           /(SQRT(ZRHO)*(ZAST+ZBST))
            ZZEPR=MAX(-ZXSEC*ZRFLC/CONFRA,ZZEPR*ZDPG)
            ZEVPC=-ZZEPR/ZDPG*DT*ZCLCSTAR
            ZEVPC=MIN(ZEVPC,MAX(ZXSEC*(ZQSW-QD(IJ,KI,NA)),0.0))
            ZEVPC=MAX(ZEVPC,0.0)
         ENDIF                  ! IPREV(1) /= 0 .AND. KI <= ISTOP
C
C        FOR STRATIFORM SNOW
         IF(IPREV(4) /= 0 .AND. KI <= IPREV(4)) THEN
C     *         MELTING OF SNOW
C
            ZRCP=1./(WCP+ZCONS1*PQDADV(IJ,KI))
            ZLSDCP=WLS*ZRCP
            ZLVDCP=WLK*ZRCP
            ZCONS=ZCONS2*ZDP/(ZLSDCP-ZLVDCP)
            ZTDIF=MAX(0.0,T(IJ,KI,NA)-TMELT)
            ZSNMLTS=MIN(ZXSEC*ZSFLS,ZCONS*ZTDIF)
            ZSFLS=ZSFLS-ZSNMLTS
            ZRFLS=ZRFLS+ZSNMLTS
C           SUBLIMATION OF SNOW (LIN ET. AL, 1983)
            IF (ZSFLS .GT. 0.0) THEN
               ZCLCSTAR=ZCLCPRE
               ZDPG=ZDP/G
               ZQRHO=1.3/ZRHO
               IT=NINT(T(IJ,KI,NA)*1000.)
CSP            HIER IST IN ECHAM EINE ABFRAGE AUF LOOKUPOVERFLOW, WENN IT
CSP            JPTLUCU1 ODER > LPTLUCU2
               IT=MAX(MIN(IT, JPTLUCU2),JPTLUCU1)
               ZESI=TLUCUA(IT)/APM1(IJ,KI)
               ZESI=MIN(ZESI, 0.5)
               ZQSI=ZESI/(1.-VTMPC1*ZESI) !*VTMPC1=RV/RD-1.
               ZSUSATI=MIN(QD(IJ,KI,NA)/ZQSI-1.0,0.0)
               ZB1=ZLSDCP**2/(2.43E-2*RD*(T(IJ,KI,NA)**2))
               ZB2=1./(ZRHO*ZQSI*0.211E-4)
               ZCOEFF=3.E6*2.*PI*ZSUSATI/(ZRHO*(ZB1+ZB2))
               IF (ZSFLS .GT. CQTMIN) THEN
                  ZXSP1=(ZSFLS/(ZCLCPRE*CVTFALL))**(1./1.16)
                  ZCLAMBS=(ZXSP1/(PI*CRHOSNO*CN0S))**0.25
                  ZCFAC4C=0.78*ZCLAMBS**2+232.19*ZQRHO**0.25
     &                 *ZCLAMBS**2.625
                  ZZEPS=MAX(-ZXSEC*ZSFLS/ZCLCPRE,ZCOEFF*ZCFAC4C*ZDPG)
                  ZSUBS=-ZZEPS/ZDPG*DT*ZCLCSTAR
                  ZSUBS=MIN(ZSUBS,MAX(ZXSEC*(ZQSI-QD(IJ,KI,NA)),0.0))
                  ZSUBS=MAX(ZSUBS,0.0)
               ENDIF
            ENDIF               ! ZCLCPRE .GT. 0.0
         ENDIF                  ! IPREV(4) /= 0 .AND. KI <= IPREV(4)
C        FOR STRATIFORM RAIN
         ISTOP   = 0            ! INIT VALUE FOR PRECIP. STOP VALUE
CBE      SET IPREV(1) to IPREV(3)
         IF(IPREV(3) /= 0) THEN
            ISTOP=IPREV(3)
         ELSE
            ISTOP=IPREV(4)
         ENDIF
         IF(ZRFLS .GT. 0.0 .AND. KI <= ISTOP) THEN
C           EVAPORATION OF RAIN  (ROTSTAYN, 1997)
C
            ZCLCSTAR=ZCLCPRE
            ZDPG=ZDP/G
            IT=NINT(T(IJ,KI,NA)*1000.)
CSP         HIER IST IN ECHAM EINE ABFRAGE AUF LOOKUPOVERFLOW, WENN
CSP         IT < JPTLUCU1 ODER > LPTLUCU2
            IT=MAX(MIN(IT,JPTLUCU2),JPTLUCU1)
            ZESW=TLUCUAW(IT)/APM1(IJ,KI)
            ZESAT=ZESW*APM1(IJ,KI)*RD/R
            ZESW=MIN(ZESW,0.5)
            ZQSW=ZESW/(1.-VTMPC1*ZESW)
            ZSUSATW=MIN(QD(IJ,KI,NA)/ZQSW-1.0,0.0)
            ZDV=2.21/APM1(IJ,KI)
            ZAST=WLK*(WLK/(RD*T(IJ,KI,NA))-1.0)/(0.024*T(IJ,KI,NA))
            ZBST=RD*T(IJ,KI,NA)/(ZDV*ZESAT)
            ZZEPR=870.*ZSUSATW*(ZRFLS/ZCLCPRE)**0.61
     &           /(SQRT(ZRHO)*(ZAST+ZBST))
            ZZEPR=MAX(-ZXSEC*ZRFLS/ZCLCPRE,ZZEPR*ZDPG)
            ZEVPS=-ZZEPR/ZDPG*DT*ZCLCSTAR
            ZEVPS=MIN(ZEVPS,MAX(ZXSEC*(ZQSW-QD(IJ,KI,NA)),0.0))
            ZEVPS=MAX(ZEVPS,0.0)
         ENDIF                 ! IPREV(3) /= 0 .AND. KI <= ISTOP
C        CALCULATE THE NEW FRACTION OF PRECIPITATION IN A GRID BOX
         ZPRETOT=ZRFLS+ZSFLS
         ZPREDEL=ZSNMLTS        ! ONLY PRODUCT IS MELTING
         LO=(ZPRETOT .GT. ZPREDEL)
         ZCLCPRE=MERGE(ZCLCPRE,MAX(ACLC(IJ,KI),CMICOV),LO)
         ZPRESUM=ZPRETOT+ZPREDEL
         IF(ZPRESUM .EQ. 0.0) THEN
            ZCLCPRE = CMICOV
         ELSE
            ZCLCPRE=MAX(ZCLCPRE,(ACLC(IJ,KI)*ZPREDEL
     &           +ZCLCPRE*ZPRETOT)/ZPRESUM)
         ENDIF
         ZCLCPRE=MIN(ZCLCPRE,1.0)
         ZCLCPRE=MAX(ZCLCPRE,CMICOV)
C        UPDATE PRECIPITATION
         IF(IPREV(1) /= 0 .AND. KI <= IPREV(1))
     &        ZRFLC=ZRFLC-ZCONS2*ZDP*ZEVPC
         IF(IPREV(2) /= 0 .AND. KI <= IPREV(2))
     &        ZSFLC=ZSFLC-ZCONS2*ZDP*ZSUBC
         IF(IPREV(3) /= 0 .AND. KI <= IPREV(3))
     &        ZRFLS=ZRFLS-ZCONS2*ZDP*ZEVPS
         IF(IPREV(4) /= 0 .AND. KI <= IPREV(4))
     &        ZSFLS=ZSFLS-ZCONS2*ZDP*ZSUBS
C
C        IF THE AMOUNT OF SNOW IS UNDER THE ADVECTION LIMIT -> MELTING
         IF(ZSFLC .GT. 0.0 .AND. ZSFLC .LT. PRELOLI) THEN
            ZRFLC  = ZRFLC  + ZSFLC
            ZLIMEL = ZLIMEL + ZSFLC
            ZSFLC  = 0.0
         ENDIF
         IF (ZSFLS .GT. 0.0 .AND. ZSFLS .LT. PRELOLI) THEN
            ZRFLS  = ZRFLS  + ZSFLS
            ZLIMEL = ZLIMEL + ZSFLS
            ZSFLS  = 0.0
         ENDIF
C        IF THE AMOUNT OF RAIN IS UNDER THE ADVECTION LIMIT -> EVAPORATION
         IF(ZRFLC .GT. 0.0 .AND. ZRFLC .LT. PRELOLI) THEN
            ZLIEVA = ZLIEVA + ZRFLC
            ZRFLC  = 0.0
         ENDIF
         IF (ZRFLS .GT. 0.0 .AND. ZRFLS .LT. PRELOLI) THEN
            ZLIEVA = ZLIEVA + ZRFLS
            ZRFLS  = 0.0
         ENDIF
C        UPDATE HUMIDITY AND TEMPERATURE
         PQTE(IJ,KI)=PQTE(IJ,KI)+(ZEVPC+ZEVPS+ZSUBC+ZSUBS+ZLIEVA)/DT
         ZRCP=1./(WCP+ZCONS1*PQDADV(IJ,KI))
         ZLSDCP=WLS*ZRCP
         ZLVDCP=WLK*ZRCP
         ZSMLT=(ZSNMLTC+ZSNMLTS+ZLIMEL)/(ZCONS2*ZDP)
         PTTE(IJ,KI)=PTTE(IJ,KI)+(ZLVDCP*(-(ZEVPC+ZEVPS+ZLIEVA))
     &        +ZLSDCP*(-(ZSUBC+ZSUBS))+(ZLSDCP-ZLVDCP)*(-ZSMLT))/DT
C
      ENDDO                    ! KI = K,MAX(IPREV(1),IPREV(2))
C     IF VERTICAL CONVECTIVE RAIN TRANSPORT, MOVE THE NEW VALUE TO HORIZONTAL
C     TRANSPORT VECTOR
      IF(IPREV(1) /= 0) THEN
         ZPREFIELD(IJ,IPREV(1),1) = ZPREFIELD(IJ,IPREV(1),1) + ZRFLC
         ZRFLC                    = 0.0
      ENDIF
C     IF VERTICAL CONVECTIVE SNOW TRANSPORT, MOVE THE NEW VALUE TO HORIZONTAL
C     TRANSPORT VECTOR (MELTING OF SNOW ALSO INCLUDED FOR RAIN)
      IF(IPREV(2) /= 0) THEN
         ZPREFIELD(IJ,IPREV(2),1) = ZPREFIELD(IJ,IPREV(2),1) + ZRFLC
         ZPREFIELD(IJ,IPREV(2),2) = ZPREFIELD(IJ,IPREV(2),2) + ZSFLC
      ENDIF
C     IF VERTICAL STARTIFORM RAIN TRANSPORT, MOVE THE NEW VALUE TO HORIZONTAL
C     TRANSPORT VECTOR
      IF(IPREV(3) /= 0) THEN
         ZPREFIELD(IJ,IPREV(3),3) = ZPREFIELD(IJ,IPREV(3),3) + ZRFLS
         ZRFLS                    = 0.0
      ENDIF
C     IF VERTICAL STRATIFORM SNOW TRANSPORT, MOVE THE NEW VALUE TO HORIZONTAL
C     TRANSPORT VECTOR (MELTING OF SNOW ALSO INCLUDED FOR RAIN)
      IF(IPREV(4) /= 0) THEN
         ZPREFIELD(IJ,IPREV(4),3) = ZPREFIELD(IJ,IPREV(4),3) + ZRFLS
         ZPREFIELD(IJ,IPREV(4),4) = ZPREFIELD(IJ,IPREV(4),4) + ZSFLS
      ENDIF
C
      ENDDO IEJELOOP
      ENDDO KLOOP
C
C
C     INITIALIZE THE HORIZONTAL ADVECTION PART
      IZT2=NZT/2
      ZZT2=NZT/2.
      IF (ZZT2.GT.REAL(IZT2)) THEN
         IXY=2
      ELSE
         IXY=1
      ENDIF
C
      IOFF  = 1
      IEOFF = IE
      JOFF  = 1
      JEOFF = JE
      ION   = 1
      IEON  = 1
      JON   = 1
      JEON  = 1
C
C     UPDATE THE HALOS FROM SURROUNDING NEIGHBORS
CKS      CALL UPDTRACHALO(ZPREFIELD,NADV,1717)
      ITYPE = 350
      KSIZE(1:10) = (/KE,KE,KE,KE,0,0,0,0,0,0/)
      CALL HALOEXCH(KSIZE, ITYPE, ZPREFIELD(:,:,1),
     &     ZPREFIELD(:,:,2), ZPREFIELD(:,:,3), ZPREFIELD(:,:,4))

C     IN ORDER TO MAKE RESTARTING OF REMO POSSIBLE (GET THE SAME RESULTS),
C     THE MOST OUTERN HALO-BOUNDARY HAS TO BE UPDATED
CKS   (COPIED FROM ECRANDAS)
C
C     1. LINKEN RAND BELEGEN
C
      IF (NEIGHBOR(1) .EQ. -1) THEN
        IA=1
        II=2
        DO J=1,JE
          IJA=IA+(J-1)*IE
          IJI=II+(J-1)*IE
          ZPREFIELD(IJA,:,1)=ZPREFIELD(IJI,:,1)
          ZPREFIELD(IJA,:,2)=ZPREFIELD(IJI,:,2)
          ZPREFIELD(IJA,:,3)=ZPREFIELD(IJI,:,3)
          ZPREFIELD(IJA,:,4)=ZPREFIELD(IJI,:,4)
        END DO
      ENDIF
C
C     2. OBEREN RAND BELEGEN
C
      IF (NEIGHBOR(2) .EQ. -1) THEN
        JA=JE
        JI=JE-1
        DO I=1,IE
          IJA=I+(JA-1)*IE
          IJI=I+(JI-1)*IE
          ZPREFIELD(IJA,:,1)=ZPREFIELD(IJI,:,1)
          ZPREFIELD(IJA,:,2)=ZPREFIELD(IJI,:,2)
          ZPREFIELD(IJA,:,3)=ZPREFIELD(IJI,:,3)
          ZPREFIELD(IJA,:,4)=ZPREFIELD(IJI,:,4)
        END DO
      ENDIF
C
C     3. RECHTEN RAND BELEGEN
C
      IF (NEIGHBOR(3) .EQ. -1) THEN
        IA=IE
        II=IE-1
        DO J=1,JE
          IJA=IA+(J-1)*IE
          IJI=II+(J-1)*IE
          ZPREFIELD(IJA,:,1)=ZPREFIELD(IJI,:,1)
          ZPREFIELD(IJA,:,2)=ZPREFIELD(IJI,:,2)
          ZPREFIELD(IJA,:,3)=ZPREFIELD(IJI,:,3)
          ZPREFIELD(IJA,:,4)=ZPREFIELD(IJI,:,4)
        END DO
      ENDIF
C
C     4. UNTEREN RAND BELEGEN
C
      IF (NEIGHBOR(4) .EQ. -1) THEN
        J=2
        DO I=1,IE
          IJ=I+(J-1)*IE
          ZPREFIELD(I,:,1)=ZPREFIELD(IJ,:,1)
          ZPREFIELD(I,:,2)=ZPREFIELD(IJ,:,2)
          ZPREFIELD(I,:,3)=ZPREFIELD(IJ,:,3)
          ZPREFIELD(I,:,4)=ZPREFIELD(IJ,:,4)
        END DO
      ENDIF
CKS
C
C     HORIZONTAL ADVECTION BY SMOLARKIEWITZ
      DO K=1,KE-1
         DO IL=1,NADV
            IF(SUM(ZPREFIELD(:,K,IL)) .NE. 0.0) CALL SHADVEC (K, DT,
     &           IXY, IL,U, V, ZPREFIELD, ACPHIR, CPHI,NADV,NA,
     &           IOFF,IEOFF,JOFF,JEOFF,ION,IEON,JON,JEON)
         ENDDO
      ENDDO
C
C     MOVE BACK THE ADVECTED FIELDS
      WHERE(ZPREFIELD(:,:,:) /= 0.0) PADVE(:,:,:) = PADVE(:,:,:) +
     &      ZPREFIELD(:,:,:)
C
      RETURN
      END SUBROUTINE RAIN_ADV
