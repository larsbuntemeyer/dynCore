C
C     SUBROUTINE DCOMPLT
C
C@(#) BIBLIOTHEK EM: EUROPA-MODELL / DEUTSCHLAND-MODELL
C@(#) MODUL DCOMPLT.F, V2.12 VOM 4/27/94, EXTRAHIERT AM 4/28/94
C
C**** DCOMPLT  -   UP:PRUEFEN, OB ALLE A- ODER R-DATEN EINGELESEN SIND.
C**   AUFRUF   :   CALL DCOMPLT(YTYP, YVARN, LVARDA, NZV, NZG, NZA)
C**   ENTRIES  :   KEINE
C**   ZWECK    :   PRUEFEN, OB ALLE A- ODER R-DATEN EINGELESEN SIND.
C**   VERSIONS-
C**   DATUM    :   09.02.89
C**
C**   EXTERNALS:   PRIV, PRCV
C**
C**   EINGABE-
C**   PARAMETER:   YTYP:   TYP DER DATEN ('A' ODER 'R')
C**                YVARN:  TABELLE MIT DEN VARIABLENNAMEN DER ZU LESEN-
C**                        DEN FELDER
C**                LVARDA: CHECK-VEKTOR(LOGICAL); WENN .TRUE., SO IST
C**                        DAS ENTSPRECHENDE FELD EINGELESEN WORDEN
C**                NZV:    DIMENSIONIERUNG VON YVARN UND LVARDA
C**                NZG:    GESAMTZAHL DER ZU LESENDEN FLAECHEN
C**                NZA:    GESAMTZAHL DER GELESENEN   FLAECHEN
C**   AUSGABE-
C**   PARAMETER:   KEINE
C**
C**   COMMON-
C**   BLOECKE  :   KEINE
C**
C**   METHODE  :   ES WIRD UEBERPRUEFT, OB ALLE LVARDA = .TRUE. SIND UND
C**                OB NZG.LE.NZA IST.
C**   FEHLERBE-
C**   HANDLUNG :   STOP IM FEHLERFALL
C**   VERFASSER:   D.MAJEWSKI
C
      SUBROUTINE DCOMPLT ( YTYP, YVARN, LVARDA, NZV, NZG, NZA )
C
      IMPLICIT NONE
C
C     Dummy Arguments
C
      INTEGER,   INTENT(IN)   ::  NZV, NZG, NZA 
      CHARACTER, INTENT(IN)   ::  YTYP*(*), YVARN(NZV)*(*)
      LOGICAL,   INTENT(IN)   ::  LVARDA(NZV)
C
C     Local Variables
C
      LOGICAL ::  LCHK
      INTEGER ::  NTAB

      LCHK = .TRUE.
      DO NTAB = 1,NZV
         IF(.NOT.LVARDA(NTAB)) LCHK = .FALSE.
      ENDDO

      IF(.NOT.LCHK .OR. NZG.GT.NZA) THEN
         PRINT 9901, YTYP, NZG, NZA
 9901    FORMAT('1    FEHLER IN *DCOMPLT*, ES SIND NICHT ALLE ',A,
     &          '-DATEN DA, STATT ',I4,'  NUR ',I4,' FELDER',/,
     &          '     VARIABLEN-NAME    DA(TRUE)',/)
         DO NTAB = 1,NZV
            PRINT 9902, YVARN(NTAB), LVARDA(NTAB)
 9902       FORMAT(' ',8X,A,7X,L1)
         ENDDO

         CALL PRIV('DCOMPLT','NZG ', NZG )
         CALL PRIV('DCOMPLT','NZA ', NZA )
         CALL PRCV('DCOMPLT','YTYP', YTYP)

         CALL REMARK(  'DCOMPLT: DATEN UNVOLLSTAENDIG' )
         STOP
      ENDIF

      RETURN
      END SUBROUTINE DCOMPLT
