C
C
C@(#) BIBLIOTHEK EM: EUROPA-MODELL / DEUTSCHLAND-MODELL
C@(#) MODUL MAKEGDB.F, V2.12 VOM 4/27/94, EXTRAHIERT AM 4/28/94
C
C**** MAKEGDB  -   UP:ERSTELLUNG DES GDB FUER EM-ERGEBNISDATEIEN
C**   AUFRUF   :   CALL MAKEGDB (YTYP)
C**   ENTRIES  :   KEINE
C**   ZWECK    :   GRID DESCRIPTION BLOCK (GDB) ERSTELLEN FUER EM-ERGEB-
C**                NISDATEIEN
C**   VERSIONS-
C**   DATUM    :   16.03.89
C**
C**   EXTERNALS:   KEINE
C**
C**   EINGABE-
C**   PARAMETER:   YTYP : TYP DER ERGEBNISDATEN:
C**                      'E': NORMALE ERGEBNISDATEN FUER GESAMTGEBIET
C**                      'T': TRAJEKTORIEN-DATEN    FUER GESAMTGEBIET
C**                      'D':         ERGEBNISDATEN FUER TEILGEBIET
C**                      'F': FORTSETZUNGSDATEN     FUER GESAMTGEBIET
C**                YNAME: NAME DER VARIABLEN
C**   AUSGABE-
C**   PARAMETER:   KEINE
C**
C**   COMMON-
C**   BLOECKE  :   PARAM, GRID, GRIB, ORG
C**
C**   METHODE  :   BESETZEN ALLER GDB-WERTE EINSCHLIESSLICH DER TABELLE
C**                DER VERTIKALKOORDINATEN-PARAMETER
C**   FEHLERBE-
C**   HANDLUNG :   KEINE
C**   VERFASSER:   D.MAJEWSKI
C
      SUBROUTINE MAKEGDB (YTYP, YNAME, MOIE, MOJE, KE1)
C
C      IMPLICIT NONE
C
      INCLUDE "grid.h"
      INCLUDE "grib.h"
      INCLUDE "org.h"
C
C     Dummy Arguments
C
      CHARACTER, INTENT(IN) :: YTYP*(*), YNAME*(*)
      INTEGER,   INTENT(IN) :: MOIE, MOJE, KE1
C
C     Local Variables
C
      INTEGER M
      REAL ZPHILU, ZRLALU
C     ---------------------------
C     GDB MIT -1 VORBESETZEN
      DO M  = 1,22
         IGDB (M) = -1
      ENDDO

C     LINKE UNTERE ECKE FESTLEGEN
      ZPHILU   = PHILU
      ZRLALU   = RLALU
      IF(YNAME.EQ.'U       ' .OR. YNAME.EQ.'BFLUS   ') THEN
         ZRLALU   = RLALU + 0.5*DLAM
      ELSE IF(YNAME.EQ.'V       ' .OR. YNAME.EQ.'BFLVS   ') THEN
         ZPHILU   = PHILU + 0.5*DPHI
      ENDIF

C     GDB BESETZEN; AUF AUSSCHNITTSGEBIET (D-DATEI) ACHTEN
      IGDB( 1) = 42 + KE1*8
      IGDB( 2) =  0
      IGDB( 3) =  0
      IGDB( 4) = 10
CKS
CKS   USE FACTOR FOR THE RESOLUTION FROM SURFACE LIBRARY
CKS
      RESFAC = FLOAT( IRESFAC )
CKS
      IF(YTYP.NE.'D') THEN
C     E-,T- UND F-FELDER UEBERDECKEN DAS GESAMTE EM-GEBIET
        IGDB( 5) = MOIE
        IGDB( 6) = MOJE
        IGDB( 7) = NINT(ZPHILU*RESFAC)
        IGDB( 8) = NINT(ZRLALU*RESFAC)
        IGDB( 9) =  128
        IGDB(10) = NINT((ZPHILU + (MOJE-1)*DPHI)*RESFAC)
        IGDB(11) = NINT((ZRLALU + (MOIE-1)*DLAM)*RESFAC)
      ELSE
C     D-FELDER UEBERDECKEN NUR EIN AUSSCHNITTSGEBIET
        IGDB( 5) = IEDGB - IADGB + 1
        IGDB( 6) = JEDGB - JADGB + 1
        IGDB( 7) = NINT((ZPHILU + (JADGB-1)*DPHI)*RESFAC)
        IGDB( 8) = NINT((ZRLALU + (IADGB-1)*DLAM)*RESFAC)
        IGDB( 9) =  128
        IGDB(10) = NINT((ZPHILU + (JEDGB-1)*DPHI)*RESFAC)
        IGDB(11) = NINT((ZRLALU + (IEDGB-1)*DLAM)*RESFAC)
      ENDIF

      IGDB(12) = NINT(DLAM*RESFAC)
      IGDB(13) = NINT(DPHI*RESFAC)
      IGDB(14) = 64
      IGDB(15) =  0
      IGDB(16) =  0
      IGDB(17) = NINT(-POLPHI*RESFAC)
      IGDB(18) = NINT((POLLAM + 180.0)*RESFAC)
C      IGDB(19) = IREFTS(0.0)
      IGDB(20) =  IRESFAC
      IGDB(21) =  0
      IGDB(22) =  0

      RETURN
      END SUBROUTINE MAKEGDB
