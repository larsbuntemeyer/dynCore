C
C
C**** PREPBOD  -   UP:AUFBEREITUNG DER MONATSWEISE VARIABLEN BODENFELDER
C**
C**   AUFRUF   :   CALL PREPBOD IN UP *EC4ORG*
C**
C**   ENTRIES  :   KEINE
C**
C**   ZWECK    :   AUFBEREITUNG DER MONATSWEISE VARIABLEN BODENFELDER
C**                ALBECH, VGRAT UND VLT
C**
C**   VERSIONS-
C**   DATUM    :   22.02.02
C**                2007
C**
C**   EXTERNALS:   KEINE
C**
C**   EINGABE-
C**   PARAMETER:   ZALB, ZVGR, ZVLT
C**
C**   AUSGABE-
C**   PARAMETER:   ALBECH, VGRAT, VLT
C**
C**   COMMON-
C**   BLOECKE  :   ORG, CORG, COMDYN, COMCON
C**
C**   METHODE  :   LINEARE ZEITLICHE INTERPOLATION ZWISCHEN DEN
C**                AUF MONATSBASIS VORHANDENEN BODENFELDERN
C**   FEHLERBE-
C**   HANDLUNG :   KEINE
C**   VERFASSER:   R.PODZUN
C
      SUBROUTINE PREPBOD(ALBECH, VGRAT, VLT, ZALB, ZVGR, ZVLT)
C
      IMPLICIT NONE
C
      INCLUDE "parorg.h"
      INCLUDE "org.h"
      INCLUDE "corg.h"
C not used      INCLUDE "COMCON"
      INCLUDE "comdyn.h"
      INCLUDE "comdia.h"
C
C     Dummy Arguments
C
      REAL, INTENT(IN)    :: ZALB(IEJE,12), ZVGR(IEJE,12), ZVLT(IEJE,12)
      REAL, INTENT(INOUT) :: ALBECH(IEJE), VGRAT(IEJE), VLT(IEJE)
C
C     Local Variables
C
      INTEGER :: MONDAY(12), MONATE(12), MONATD(12)
      INTEGER :: I100, I4, I400, ID, IDAYL, IH, IJ, IM, IMM1, IMM1SEC, 
     &           IMP1, IMP1SEC, IMSECA, IMSECB, ISCHLT, ISEC, IY, M
      REAL    :: ZMONSECA, ZMONSECB, ZSECA, ZSECB, ZW1, ZW2
C
      DATA  MONATD / 30 ,  30 ,  30 ,  30 ,  30 ,  30 ,
     &               30 ,  30 ,  30 ,  30 ,  30 ,  30 /
      DATA  MONATE / 31 ,  28 ,  31 ,  30 ,  31 ,  30 ,
     &               31 ,  31 ,  30 ,  31 ,  30 ,  31 /
C
      IDAYL=86400
C
      CALL DATUTC(NZT, YADAT, DT, YAKDAT1, YAKDAT2, NAKJATA,
     &            AKHH)
C
C     AKTUELLES DATUM EINLESEN
C
      READ(YAKDAT1,'(I4,3I2)') IY,IM,ID,IH
C
C     JE NACH ANTRIEB ZAHL DER TAGE IM MONAT BERECHNEN
C
      IF (LMOMON) THEN
         DO M=1,12
            MONDAY(M)=MONATD(M)
         ENDDO
      ELSE
         DO M=1,12
            MONDAY(M)=MONATE(M)
         ENDDO
C
C        SCHALTJAHR BESTIMMEN
C
         ISCHLT=0
         I4=MOD(IY,4)
         I100=MOD(IY,100)
         I400=MOD(IY,400)
         IF (I4.EQ.0) ISCHLT=1
         IF (I100.EQ.0) ISCHLT=0
         IF (I400.EQ.0) ISCHLT=1
         MONDAY(2)=MONDAY(2)+ISCHLT
      ENDIF
C
C     JAHRESUEBERSCHREITUNGEN BERUECKSICHTIGEN
C
      IMM1=IM-1
      IF (IM.EQ.1) IMM1=12
      IMP1=IM+1
      IF (IM.EQ.12) IMP1=1
C
C     ZAHL DER SEKUNDEN DER ZWEITEN MONATSHAELFTE DES VORMONATS
C
      IMM1SEC=(MOD(MONDAY(IMM1),2)*IDAYL)+((MONDAY(IMM1)/2)*IDAYL)
C
C     ZAHL DER SEKUNDEN DER ERSTEN MONATSHAELFTE DES AKTUELLEN MONATS
C
      IMSECA=(MONDAY(IM)/2)*IDAYL
C
C     ZAHL DER SEKUNDEN DES GESAMTEN INTERVALLS
C     (16.(VORMONAT) - 15.[INKL.] (AKTUELLER MONAT))
C
      ZMONSECA=FLOAT(IMM1SEC+IMSECA)
C
C     ZAHL DER SEKUNDEN DER ZWEITEN MONATSHAELFTE DES AKTUELLEN MONATS
C
      IMSECB=(MOD(MONDAY(IM),2)*IDAYL)+((MONDAY(IM)/2)*IDAYL)
C
C     ZAHL DER SEKUNDEN DER ERSTEN MONATSHAELFTE DES FOLGEMONATS
C
      IMP1SEC=(MONDAY(IMP1)/2)*IDAYL
C
C     ZAHL DER SEKUNDEN DES GESAMTEN INTERVALLS
C     (16.(AKTUELLER MONAT) - 15.[INKL.] (FOLGEMONAT))
C
      ZMONSECB=FLOAT(IMP1SEC+IMSECB)
C
C     ZAHL DER SEKUNDEN DES AKTUELLEN DATUMS BERECHEN
C
      ISEC=(ID-1)*IDAYL+IH*3600
C
C     ZAHL DER SEKUNDEN DES AKTUELLEN DATUMS IM JEWEILIGEN INTERVALL
C
      ZSECA=FLOAT(ISEC+IMM1SEC)
      ZSECB=FLOAT(ISEC-IMSECA)
C
C     BERECHNUNG DER BODENFELDER
C
C     ERSTE MOANTASHAELFTE
C
      IF (ID.LE.15) THEN
C
         ZW1=ZSECA/(ZMONSECA)
         ZW2=1.-ZW1
         DO IJ=1,IEJE
            ALBECH(IJ)=ZW1*ZALB(IJ,IM)+ZW2*ZALB(IJ,IMM1)
            VGRAT (IJ)=ZW1*ZVGR(IJ,IM)+ZW2*ZVGR(IJ,IMM1)
            VLT   (IJ)=ZW1*ZVLT(IJ,IM)+ZW2*ZVLT(IJ,IMM1)
         ENDDO
C
C     ZWEITE MOANTASHAELFTE
C
      ELSE
C
         ZW2=ZSECB/(ZMONSECB)
         ZW1=1.-ZW2
         DO IJ=1,IEJE
            ALBECH(IJ)=ZW1*ZALB(IJ,IM)+ZW2*ZALB(IJ,IMP1)
            VGRAT (IJ)=ZW1*ZVGR(IJ,IM)+ZW2*ZVGR(IJ,IMP1)
            VLT   (IJ)=ZW1*ZVLT(IJ,IM)+ZW2*ZVLT(IJ,IMP1)
         ENDDO
C
      ENDIF
C
      RETURN
      END SUBROUTINE PREPBOD
