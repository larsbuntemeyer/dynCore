      REAL FUNCTION  RLSTORL ( PHIS,RLAS,POLPHI,POLLAM )
C
C@(#) BIBLIOTHEK EM: EUROPA-MODELL / DEUTSCHLAND-MODELL
C@(#) MODUL RLSTORL.F, V2.12 VOM 4/27/94, EXTRAHIERT AM 4/28/94
C
C**** RLSTORL  -   FC:UMRECHNUNG VON RLAS (ROT.SYSTEM) AUF RLA(GEOG.SY.)
C**   AUFRUF   :   RLA = RLSTORL(PHIS,RLAS,POLPHI,POLLAM)
C**   ENTRIES  :   KEINE
C**   ZWECK    :   UMRECHNUNG DES RLAS DES ROTIERTEN SYSTEMS IN RLA
C**                (IN GEOGRAPHISCHEN KOORDINATEN).
C**   VERSIONS-
C**   DATUM    :   13.02.89
C**
C**   EXTERNALS:   KEINE
C**
C**   EINGABE-
C**   PARAMETER:   PHIS:   BREITE IM ROTIERTEN SYSTEM (N>0)
C**                RLAS:   LAENGE IM ROTIERTEN SYSTEM (E>0)
C**                POLPHI: GEOGRAPHISCHE BREITE DES N-POLS DES ROT. SYS.
C**                POLLAM: GEOGRAPHISCHE LAENGE DES N-POLS DES ROT. SYS.
C**   AUSGABE-
C**   PARAMETER:   GEOGRAPHISCHE LAENGE ALS WERT DER FUNCTION
C**
C**   COMMON-
C**   BLOECKE  :   KEINE
C**
C**   METHODE  :   ANWENDUNG DER TRANSFORMATIONSFORMELN ZWISCHEN DEM
C**                ROTIERTEN UND UNROTIERTEM SYSTEM
C**   FEHLERBE-
C**   HANDLUNG :   KEINE
C**   VERFASSER:   D.MAJEWSKI
C
      IMPLICIT NONE
C
C     Dummy Arguments
C
      REAL   :: PHIS,RLAS,POLPHI,POLLAM
C
C     Local Variables
C
      REAL   :: ZARG1, ZARG2, ZCOSPOL, ZLAMPOL, ZPHIS, ZPIR18, ZRLAS, 
     &          ZSINPOL, ZRPI18
C
      DATA        ZRPI18 , ZPIR18  / 57.2957795 , 0.0174532925 /
C
      ZSINPOL = SIN(ZPIR18*POLPHI)
      ZCOSPOL = COS(ZPIR18*POLPHI)

      ZLAMPOL= ZPIR18*POLLAM
      ZPHIS  = ZPIR18*PHIS
      IF(RLAS.GT.180.0) RLAS = RLAS - 360.0
      ZRLAS  = ZPIR18*RLAS

      ZARG1   = SIN(ZLAMPOL)*(- ZSINPOL*COS(ZRLAS)*COS(ZPHIS)  +
     &                          ZCOSPOL*           SIN(ZPHIS)) -
     &          COS(ZLAMPOL)*           SIN(ZRLAS)*COS(ZPHIS)
      ZARG2   = COS(ZLAMPOL)*(- ZSINPOL*COS(ZRLAS)*COS(ZPHIS)  +
     &                          ZCOSPOL*           SIN(ZPHIS)) +
     &          SIN(ZLAMPOL)*           SIN(ZRLAS)*COS(ZPHIS)
      IF(ZARG2.EQ.0.0) ZARG2 = 1.0 E-20

      RLSTORL = ZRPI18*ATAN2(ZARG1,ZARG2)

      RETURN
      END FUNCTION RLSTORL
