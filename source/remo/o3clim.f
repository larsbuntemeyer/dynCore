      SUBROUTINE O3CLIM(KLON, KLEV, NOZ, PPH, PPF, OZONPL, OZONML)
C
      IMPLICIT NONE
C
      INTEGER,                      INTENT(IN)  :: KLON,KLEV,NOZ
      REAL, DIMENSION(KLON,KLEV),   INTENT(IN)  :: PPH
      REAL, DIMENSION(KLON,KLEV+1), INTENT(IN)  :: PPF
      REAL, DIMENSION(KLON,NOZ),    INTENT(IN)  :: OZONPL
      REAL, DIMENSION(KLON,KLEV),   INTENT(OUT) :: OZONML
C
C     - DESCRIPTION:
C
C     THE OZONE ZONAL MEAN AND/OR MONTHLY MEAN CLIMATOLOGY IS INTERPOLATED
C     TO THE ACTUAL TIME OF THE INTEGRATION AND INTEGRATED FROM P=0 TO
C     THE SURFACE P=PS.
C     THE TIME INTERPOLATED PROFILE OF OZONE IS INTERPOLATED TO THE
C     MODEL FULL LEVELS AND INTEGRATED AGAIN FROMP=0 TO P=PS.
C     FINALLY THE OZONE PROFILE ON THE MODEL LEVELS IS NORMALIZED SUCH
C     THAT THE INTEGRATED AMOUNT ON THE MODEL GRID IS IDENTICAL TO
C     THAT ON THE GRID OF THE CLIMATOLOGY.
C
C     - AUTHOR:
C
C     M.A. GIORGETTA, MPI, MAY 2000
C
C     INPUT
C     -----
C
C     KLON  : FIRST DIMENSION OF 2-D ARRAYS
C     KLEV  : NUMBER OF LEVELS
C     PPF   : FULL LEVEL PRESSURE
C     PPH   : HALF LEVEL PRESSURE
C     OZONPL: OZONE ON PRESSURE LEVELS
C
C      DIMENSION PPF(KLON,KLEV)
C      DIMENSION PPH(KLON,KLEV+1)
C      DIMENSION OZONPL(KLON,NOZ)
C
C     OUTPUT
C     ------
C     OZONML : OZONE IN G/G
C
C      DIMENSION OZONML(KLON,KLEV)
C
C     LOCAL
C     -----
C     PRESSURE INTEGRATED OZONE AT HALF LEVELS OF OZONE GRID,
C     AND INTEGRAL AT SURFACE
C
C      DIMENSION ZOZINTC(KLON)
      REAL :: ZOZINTC(KLON)
C
C     TIME INTERPOLATED OZONE AT FULL LEVELS OF MODEL GRID,
C     PRESSURE INTEGRATED OZONE AT HALF LEVELS OF MODEL,
C     AND INTEGRAL AT SURFACE
C
      REAL :: ZOZONEM(KLON,KLEV)
      REAL :: ZOZINTM(KLON)
C
C     ZDP1,ZDP2: PRESSURE WEIGHTS IN LINEAR INTERPOLATION
C     JL,JK,JKK: LOOP INDICES
C     JK1,JKN  : FIRST AND LAST MODEL LEVEL IN INTERPOLATION
C
      INTEGER :: JK1(KLON),JKN(KLON)
C
      INTEGER :: KWORK(KLON)
      LOGICAL :: LK_FLAG(KLON)
C
      INTEGER :: IJ,JK,JL,JKK
      REAL    :: DP,ZDP1,ZDP2
C
      INCLUDE "COMOZON"
C
CKK   NEC VERSION                                      KLAUS KETELSEN
CKK   1. REMOVE EXIT FROM LOOP FOR VECTORIZING
CKK   2. DO JL=1,KLON AS INNER LOOP TO GET SUFFICIANT VECTOR LENGTH
C
CKK   NOW SOME LOOPS HAVE HIGHER OPERATION COUT, BUT THEY ARE VECTORIZING
CKK   THE LOGIC IS THE SAME AS IN THE ORIGINAL VERSION USING EXIT IN LOOPS
C
C     INTERPOLATE OZONE PROFILE TO MODEL GRID
C     ---------------------------------------
C     SET OZONE CONCENTRATION AT LEVELS ABOVE THE UPPERMOST LEVEL OF
C     THE OZONE CLIMATOLOGY TO THE VALUE IN THE UPPERMOST LEVEL OF
C     THE OZONE CLIMATOLOGY
C
      DO IJ=1,KLON
         JK1(IJ)=1
         LK_FLAG(IJ)=.TRUE.
      ENDDO
C
      DO JK = 1,KLEV
         DO JL=1,KLON
            IF (PPF(JL,JK).LE.PFOZ(1) .AND. LK_FLAG(JL)) THEN
               ZOZONEM(JL,JK)=OZONPL(JL,1)
               JK1(JL)=JK+1
            ELSE
               LK_FLAG(JL) = .FALSE.
            ENDIF
         ENDDO
      ENDDO
C
C     SET OZONE CONCENTRATION AT LEVELS BELOW THE LOWERMOST LEVEL OF
C     THE OZONE CLIMATOLOGY TO THE VALUE IN THE LOWERMOST LEVEL OF
C     THE OZONE CLIMATOLOGY
C
      DO IJ=1,KLON
         JKN(IJ)=KLEV
         LK_FLAG(IJ)=.TRUE.
      ENDDO
C
      DO JK = KLEV,1,-1
         DO JL=1,KLON
            IF (PPF(JL,JK).GE.PFOZ(NOZ).AND. LK_FLAG(JL)) THEN
               ZOZONEM(JL,JK)=OZONPL(JL,NOZ)
               JKN(JL)=JK-1
            ELSE
               LK_FLAG(JL) = .FALSE.
            ENDIF
         ENDDO
      ENDDO
C
      DO JK=1,KLEV
C
         DO IJ=1,KLON
            LK_FLAG(IJ)=.TRUE.
            KWORK(IJ)=1
         ENDDO
C
         DO JKK = 1,NOZ
            DO JL=1,KLON
               IF(JK.GE.JK1(JL) .AND. JK.LE.JKN(JL))  THEN
                  IF (PPF(JL,JK).LE.PFOZ(JKK) .AND. JKK.GE.KWORK(JL)
     1                                        .AND. LK_FLAG(JL)) THEN
                     KWORK(JL)   = JKK
                     LK_FLAG(JL) = .FALSE.
                  ENDIF
               ENDIF
            ENDDO
         ENDDO
C
         DO JL=1,KLON
            IF(JK.GE.JK1(JL) .AND. JK.LE.JKN(JL))  THEN
               JKK = KWORK(JL)
C                 MODEL LEVEL IS IN INTERVAL ]PFOZ(JKK-1),PFOZ(JKK)]
C                 -> MAKE INTERPOLATION
               ZDP1=PFOZ(JKK)-PPF(JL,JK)
               ZDP2=PPF(JL,JK)-PFOZ(JKK-1)
               ZOZONEM(JL,JK)=(ZDP1*OZONPL(JL,JKK-1)+ZDP2*
     &              OZONPL(JL,JKK))/(ZDP1+ZDP2)
            ENDIF
         ENDDO
C
      ENDDO
C
C     INTEGRATE OZONE PROFILE ON GRID OF CLIMATOLOGY
C     FROM TOP TO SURFACE
C     ----------------------------------------------
C
      DO IJ=1,KLON
         LK_FLAG(IJ)=.TRUE.
         JK1(IJ)=2
         ZOZINTC(IJ)=0.
      ENDDO
C
      DO JK=2,NOZ+1
C     INTEGRATE LAYERS OF CLIMATOLOGY ABOVE SURFACE
         DO JL=1,KLON
            IF (PHOZ(JK).LE.PPH(JL,KLEV) .AND. LK_FLAG(JL) ) THEN
               ZOZINTC(JL)=ZOZINTC(JL)+
     &              OZONPL(JL,JK-1)*(PHOZ(JK)-PHOZ(JK-1))
               JK1(JL) = JK+1
            ELSE
               LK_FLAG(JL) = .FALSE.
            ENDIF
         ENDDO
      ENDDO
C
C     INTEGRATE LAYER OF CLIMATOLOGY THAT IS INTERSECTED
C     BY THE SURFACE FROM UPPER BOUNDARY TO SURFACE
C
      DO JL=1,KLON
         ZOZINTC(JL)=ZOZINTC(JL)+
     &        OZONPL(JL,JK1(JL)-1)*(PPH(JL,KLEV)-PHOZ(JK1(JL)-1))
      ENDDO
C
C     INTEGRATE OZONE PROFILE ON GRID OF MODEL
C     FROM TOP TO SURFACE
C     ----------------------------------------
C
      DO IJ=1,KLON
         ZOZINTM(IJ)=0.
      ENDDO
C
      DO JK=2,KLEV+1
         DO JL=1,KLON
            ZOZINTM(JL)=ZOZINTM(JL) + ZOZONEM(JL,JK-1)*
     &           (PPH(JL,JK)-PPH(JL,JK-1))
         ENDDO
      ENDDO
C
C     NORMALIZE INTERPOLATED OZONE PROFILE SUCH THAT THE
C     OZONE INTEGRAL COMPUTED ON THE MODEL GRID IS EQUAL
C     TO THAT INTEGRATED ON THE GRID OF THE CLIMATOLOGY
C     --------------------------------------------------
C
      DO JK=1,KLEV
         DO JL=1,KLON
            OZONML(JL,JK)=ZOZONEM(JL,JK)/ZOZINTM(JL) * ZOZINTC(JL)
         ENDDO
      ENDDO
C
C     CONVERT FROM MASS MIXING RATIO TO PA/PA TO BE USED
C     WITH ECHAM4 RADIATION
C
      DO JK=1,KLEV
         DO JL=1,KLON
            DP=(PPH(JL,JK+1)-PPH(JL,JK))
            OZONML(JL,JK)=OZONML(JL,JK)*DP
         ENDDO
      ENDDO
C
      RETURN
      END
