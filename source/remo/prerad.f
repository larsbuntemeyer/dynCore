C
C
C**** *PRERAD* - *PRESET SOME CONSTANTS FOR RADIATION BEFORE FIRST
C                 LINE OF LATITUDE.
C
C     M.JARRAUD    14 JUIN 1983.
C
C     PURPOSE.
C     --------
C
C          THIS SUBROUTINE COMPUTES SOME PARAMETERS FOR THE RADIATION
C     SCHEME WHICH REMAIN UNCHANGED DURING SCAN OVER LATITUDE LINES.
C
C**   INTERFACE.
C     ----------
C
C          *PRERAD* IS CALLED FROM RADCY_43*
C
C     EXTERNALS.
C     ----------
C
C          *ORBIT*   COMPUTE ORBITAL PARAMETERS.
C          *OZONE*   COMPUTE PARAMETERS FOR OZONE.
C          *AEROSOL* COMPUTE PARAMETERS FOR AEROSOLS.
C
      SUBROUTINE PRERAD( NSTEP,
CHL   ---REPLACE PART OF *COMCTL*
     &     DTIME  ,
     &     NCBASE , NTBASE  ,
     &     NTIMST , NMONTH  ,
CHL   ---REPLACE PART OF *COMRSW*
     &     NRADFR)
C
      IMPLICIT NONE
C
      INCLUDE "COMCON"
      INCLUDE "COMRAD1"
      INCLUDE "COMPH1"
C
C     Dummy Arguments
C
      INTEGER, INTENT(IN) :: NSTEP,  NCBASE, NTBASE,
     &                       NTIMST, NMONTH, NRADFR
      REAL,    INTENT(IN) :: DTIME
C
C     Local Variables
C
      REAL :: ZCLOCK, ZYTIME, ZZYTIME
C     --------------------------------------------------------------
C
C*         1.     COMPUTE ORBITAL,OZONE AND AEROSOL PARAMETERS
C                 ------- ------- ----- --- ------- ----------
C
      IF(NMONTH.NE.0) THEN
         ZZYTIME=(NMONTH*30-15)/YEARL*2.*API
      ENDIF
C
C
      ZCLOCK=AMOD((NTBASE*NTIMST+NSTEP*DTIME)/DAYL,1.)*2.*API
      ZYTIME=AMOD(((NCBASE-1.)+(NTBASE*NTIMST+NSTEP*DTIME)/DAYL)
     &     /YEARL,1.)*2.*API
C
      IF(NMONTH.NE.0) THEN
         ZYTIME=ZZYTIME
      ENDIF
C
      CALL ORBIT(ZCLOCK,ZYTIME,CDISSE,CZEN1,CZEN2,CZEN3,CRAE)
      IF (NSTEP.EQ.0.OR.MOD(NSTEP+1,NRADFR).EQ.0) THEN
         CALL OZONE(ZYTIME,COZQC,COZQS,COZHC,COZHS)
         CALL ECAEROS(CAESC,CAESS,CAELC,CAELS,CAEUC,CAEUS,CAEDC,CAEDS)
C
C     COMPUTE ORBITAL PARAMETERS FOR FULL RADIATION TIME STEP.
C
         ZCLOCK=AMOD((NTBASE*NTIMST+NSTEP*DTIME+0.5*NRADFR*DTIME)/
     &        DAYL,1.)*2.*API
         ZYTIME=AMOD(((NCBASE-1.)+(NTBASE*NTIMST+NSTEP*DTIME+
     &        0.5*NRADFR*DTIME)/DAYL)/YEARL,1.)*2.*API
C
         IF(NMONTH.NE.0) THEN
            ZYTIME=ZZYTIME
         ENDIF
C
         CALL ORBIT(ZCLOCK,ZYTIME,CDISSEM,CZEN1M,CZEN2M,CZEN3M,CRAE)
      ENDIF
C
      RETURN
      END
