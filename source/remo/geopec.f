C
C                 SUBROUTINE GEOPEC
C
C**** *GEOPOT* - *CALCULATES FULL- OR HALF-LEVEL GEOPOTENTIALS.
C
C     A.J.SIMMONS     E.C.M.W.F.    19/01/82.
C
C     PURPOSE
C     -------
C
C             *TO INTEGRATE THE HYDROSTATIC EQUATION IN THE VERTICAL
C     TO OBTAIN FULL- OR HALF-LEVEL VALUES OF THE GEOPOTENTIAL.
C
C**   INTERFACE
C     ---------
C
C             *GEOPOT* IS CALLED DURING THE CALCULATION OF ADIABATIC
C     TENDENCIES, PRIOR TO THE CALCULATION OF THE PHYSICAL PARAMET-
C     ERIZATIONS, AND IN THE POST-PROCESSING. PARAMETERS ARE
C
C             *PHI*         *COMPUTED GEOPOTENTIAL.
C             *PTV*         *VIRTUAL TEMPERATURE.
C             *PLNPR*       *LOGARITHM OF RATIO OF PRESSURES, COMPUTED
C                           BY *AUXHYB.*
C             *PALPHA*      *FOR FULL-LEVEL VALUES USE *ALPHA* AS
C                           COMPUTED BY *AUXHYB.* SET *ALPHA* TO
C                           ZERO FOR VALUES AT HALF-LEVELS BELOW
C                           THE FULL LEVELS.
C             *PHIS*        *SURFACE GEOPOTENTIAL.
C             *KDIM*        *FIRST DIMENSION OF 2-D ARRAYS *PHI,*
C                           *PTV,* *PLNPR,* AND *PALPHA.*
C
C             *REQUIRED CONSTANTS ARE OBTAINED FROM COMMON BLOCKS
C     *COMSRR* AND *COMHYB.* THE LATTER SHOULD HAVE BEEN
C     INITIALIZED BY A CALL OF SUBROUTINE *INIHYB.*
C
C     RESULTS
C     -------
C
C             *RESULTS ARE COMPUTED FOR  CONSECUTIVE POINTS FOR
C     *NLEV* LEVELS.
C
C     METHOD
C     ------
C
C             *THE CHOICE OF FULL- OR HALF-LEVEL VALUES IS DETERMINED
C     BY THE SPECIFICATION OF THE INPUT ARRAY *ALPHA.*
C
C     REFERENCE
C     ---------
C
C             *EXTERNAL DOCUMENTATION OF THE MODEL EQUATIONS AND THE
C     ORGANIZATION OF THE VERTICAL CALCULATION.
C
C
      SUBROUTINE GEOPEC(KDIM,KSTART,KSTOP,NLEV,NLEVM1,
     &                  PHI,PTV,PLNPR,PALPHA,PHIS)
C
      IMPLICIT NONE
C
C     Dummy Arguments
C
      INTEGER, INTENT(IN)    :: KDIM,KSTART,KSTOP,NLEV,NLEVM1
      REAL,    INTENT(IN)    :: PTV(KDIM,NLEV),PLNPR(KDIM,NLEV),
     &                          PALPHA(KDIM,NLEV),PHIS(KDIM)
      REAL,    INTENT(INOUT) :: PHI(KDIM,NLEV)
C
C     Local Variables
C
      INTEGER :: JL,JK,IKP
C
!DIR$ NOBOUNDS
C     ------------------------------------------------------------------
C
C*        1.    INTEGRATE HYDROSTATIC EQUATION.
C               --------- ----------- ---------
C
      DO JL=KSTART,KSTOP
         PHI(JL,NLEV)=PALPHA(JL,NLEV)*PTV(JL,NLEV)+PHIS(JL)
      ENDDO
C
      DO JK=NLEVM1,1,-1
         IKP=JK+1
C
         DO JL=KSTART,KSTOP
            PHI(JL,JK)=PALPHA(JL,JK)*PTV(JL,JK)+
     &           (PLNPR(JL,IKP)-PALPHA(JL,IKP))*PTV(JL,IKP)+PHI(JL,IKP)
         ENDDO
C
      ENDDO
C
C     ------------------------------------------------------------------
C
      RETURN
      END SUBROUTINE GEOPEC
